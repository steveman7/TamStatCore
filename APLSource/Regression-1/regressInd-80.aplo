 regressInd←{
⍝∇ Regress using indicator  variables
⍝
⍝⍺  Y or Name
⍝⍺⍺ Int
⍝⍵  C X1 .. Xn   or Database
⍝⍵⍵ 1=Character vectors are indicator variables
⍝⍵⍵ 0=Character vectors are ignored
⍝←
     ⎕ML←1 ⋄ ⎕IO←0          ⍝ Default values
     DB←9=⎕NC'⍵'            ⍝ Is right argument a database?
     W←DB{⍺:⍵⍎¨variables ⍵  ⍝ Obtain data values
         ↑¨⊆⍵}⍵             ⍝
     B←{0≢⊃0⍴⍵}¨W           ⍝ Find character fields
     Y←⍺{0=⊃0⍴⍺:⍺ ⋄ ⍵⍎⍺}⍵   ⍝ Response Variable
     U←Y∘{0::''
         M←⍺{⍺,⍥⊂(+/÷≢)⍵}⌸⍨⍵
         ↑⊃M[⍋M[;1];0]}¨B/W
     U←(⍺⍺≡0)⊃U ⍺⍺          ⍝ If no intercept, keep all indicator variables
     NS←(⍵⍵∧∨/B){⍺:⊃U makeIndicator¨⍵ ⋄ ⎕NS''}B/W ⍝ Put indicator variables into namespace
     X←{⊃⍣(1=≢⍵)⊢⍵}(~B)/W                         ⍝ Get values of non-indicator variables
     NMS←DB B{DB B←⍺                              ⍝ Get names of non-indicator variables
         DB:(~B)/variables ⍵
         'X',¨(B+.=0)↑1↓⎕D}⍵
   ⍝  _←⍎⍕NMS'←X'                                  ⍝ Else assign to names in char field
     _←{0=≢⍵:0 ⋄ ⍎⍕⍵'←X'}NMS
     _←'NS'⎕NS NMS
     NMS←variables NS
     DB:⍺⍺ leastSquares NS,⊂⍕NMS(⊢,⊣~⊢)⊂⍺
     NS.Y←⍺
     Z←⍺⍺ leastSquares NS(⍕'Y',NMS)

     ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝ Create function???
     ⍝Z.g←Deg M{⍵≡'Type':'Linear'
⍝         ⍺←0                                ⍝ Default
⍝         CL A←2↑⍺                           ⍝ Action, Conf Level
⍝         D M←⍺⍺
⍝         X←D{1=≢M:⍵∘.*(~⎕IO)+⍳⍺
⍝             ∊(×/⍵)pairwise↓⍵∘.*1 2}⍵-M
⍝         A=0:f X
⍝         ⍺ f X}
     Z



 }
