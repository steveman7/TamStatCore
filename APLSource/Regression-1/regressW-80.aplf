 regressW←{
     ⍝ Weighted Least Squares
     ⍝ ⍺ List of weights
     ⍝ ⍵ y X1 ... Xn
     ⎕IO←0 ⋄ ⎕ML←3
     W←⍺ ⋄
     X←1⍪1↓⊃⍉⍵                                        ⍝ Predictor Variables
     N←≢y←↑⍵ ⋄ P←≢⍵                                   ⍝ Response Variable
     XW←X×⍤1⊢W
     B←(⌹XW+.×⍉X)+.×XW+.×y
     BB←X×⍤1 sqrt W
     z←y×sqrt W
     SST←z+.×z
     SSR←B+.×BB+.×z
     SSE←SST-SSR
     k←≢BB ⋄ DF←N-k
     C←⌹XW+.×⍉X
     V←÷W                                               ⍝ Covariance Matrix
     E←y-YHAT←B+.×X                                   ⍝ Residuals
     SSX←1↓1{(⍵+.*2)-⍺×N÷⍨(+/⍵)*2}X             ⍝??
     XBAR←⍵+.×¨⊂⍺÷+/⍺                                    ⍝ Means for each X
     MSE←SSE÷DF                                       ⍝ Mean Square Error
     S←(leastSquares ⍵).S                             ⍝ Get S from non-weighted model.
     SB←sqrt 0 0⍉(S*2)×⌹BB+.×⍉BB                    ⍝ Montgomery p. 237 and 362
     ns←'Regression'buildAnovaTable SSR SSE,k DF,⍪2 0 ⍝ Create ANOVA table
     T←B÷SB                                           ⍝ Test statistic
     P←2×DF tDist probability>|T                      ⍝ p-Value
     YX←y,⍉1↓X
     H←(⍉X)+.×C+.×X                                   ⍝ Hat Matrix
     R←E÷S÷sqrt 1-0 0⍉H                              ⍝ Studentized Residuals
     VIF←0 0⍉⌹XX←corr 1↓↓X                          ⍝ Variance inflation factor
     ConditionNumber←(⌈/÷⌊/)eigenValues XX            ⍝ Condition Number
     PR←'B,T,P,N,XBAR,SSX,DF,S,MSE,SB,E,YHAT,YX,C,VIF,R,H' ⍝ Put additional
     PR,←',ConditionNumber'
     _←'ns'⎕NS toNestedVector PR                      ⍝  properties into namespace
     ns
 }
