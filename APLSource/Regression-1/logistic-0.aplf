 logistic←{
⍝∊ R Febrianti, et al 2021 J. Phys: Conf Series 1725 012014
⍝∇ Calculate logistic regression model
⍝⍺ Y - Response Variable (Boolean)
⍝⍺ X - Predictor Variable
⍝⍵ Intercept (Guess)
⍝⍵ Slope  (Guess)
⍝← Coefficients
⍝← Function
⍝⍎ NS←(Y,⍪X) logistic Intercept Slope

     ⎕IO←0 ⋄ ⎕ML←1             ⍝ Initialize
     Y X←↓⍉⍺                   ⍝ Response, predictor variables
    ⍝ Y←⍺[;0]  ⋄ X←0 1↓⍺
     Beta←⊃⊆⍵
     Pi←÷1+*-(⍪X)⊥⌽Beta
     ⍝Pi←÷1+*-X+.×Beta
   ⍝  U←(+/Y-Pi)(+/X×Y-Pi)
     A←+/Pi×1-Pi
     B←+/X×Pi×1-Pi
     C←+/(X*2)×Pi×1-Pi
   ⍝  IB←-⌹2 2⍴A B B C
     JB←2 2⍴A B B C
   ⍝  ⎕←B←⍵-IB+.×U
   ⍝  B
     W←(∘.=⍨⍳≢Y)×⍤1⊢Pi×1-Pi
     XX←1,⍪X
     WX←(sqrt W)+.×XX
     JBINV←⌹JB
     H←0 0⍉WX+.×JBINV+.×⍉WX
     Xi←H×(Pi-0.5)÷Pi×1-Pi
     bB←JBINV+.×(⍉XX)+.×W+.×Xi
  ⍝  UB←U-JB+.×bB
     UU←(Y+H÷2)-Pi×1+H
     UB←(+/UU)(UU+.×X)
     Beta+←bB+JBINV+.×UB
     Beta JBINV H Pi

 }
