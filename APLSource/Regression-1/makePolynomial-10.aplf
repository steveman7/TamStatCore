 makePolynomial←{
⍝∇  Generate polynomial variables from a character field
⍝⍺ Left argument indicates degree of Polynomial or X1
⍝⍵ Right argument is variable
⍝← Indictor variable(s)
     ⎕IO←0 ⋄ ⎕ML←1 ⋄ ⍺←2          ⍝ Default is quadratic
     I←(1=≢⍺)∧(0∘<∧⌊=⊢)⊃⍺         ⍝ Is left argument integer?
     X←(⊢-+/÷≢)¨I↓⍺ ⍵             ⍝ Center the data
     K←I⊃2 ⍺                      ⍝ Degree of polynomial
     XX←(,⍉X∘.*1+⍳K),×pairwise X  ⍝ Generate variables
     NMS←,⌽⍉I↓'YX'∘.,K↑1↓⎕D       ⍝ Generate Names
     NMS,←I↓⊂'XY'                 ⍝
     _←⍎⍕NMS'←XX'                 ⍝ Assign values
     _←⎕EX'#.∆NS'                 ⍝ Clear namespace
     _←'#.∆NS'⎕NS NMS             ⍝ Create namespace
     #.∆NS
 }
