 runRegress←{
     ⎕IO←0 ⋄ ⎕ML←3
     int←⍺ ⋄ W←⍵
     X←(~int)↓1⍪1↓⊃⍉W                                 ⍝ Predictor Variables
     N←≢y←↑W                                          ⍝ Sample Size
     C←⌹X+.×⍉X                                        ⍝ Covariance Matrix
     k←(¯1×int)+≢B←C+.×Xy←X+.×y                       ⍝ Estimate coefficients
     SSE←(y+.*2)-B+.×Xy                               ⍝ Error Sum of Squares
     E←y-YHAT←B+.×X                                   ⍝ Residuals
     ss←{(⍵+.*2)-⍺×N÷⍨(+/⍵)*2}                        ⍝ SumSquares function
     SST←int ss y                                     ⍝ Total sum of Squares
     SSX←1↓int ss X                                   ⍝ Why is this here?
     XBAR←mean¨W                                      ⍝ Means for each X
     DF←N-k+int                                       ⍝ Degrees of Freedom
     MSE←SSE÷DF                                       ⍝ Mean Square Error
     S←MSE*÷2                                         ⍝ Standard error for regression
     SB←(MSE×0 0⍉C)*÷2                                ⍝ Standard Error for coeffs
     SSR←SST-SSE                                      ⍝ Regression sum of squares
     ns←'Regression'buildAnovaTable SSR SSE,k DF,⍪2 0 ⍝ Create ANOVA table
     ns.AnovaTable←ns.AnovaTable{2≠⍴⍵:⍺               ⍝ If multple reg or no dups, exit
         0∧.=PE←+⌿↑{(2+.*⍨⍵-mean ⍵),¯1+≢⍵}⌸/⌽⍵:⍺      ⍝ Calculate Pure Error SS,DF
         ERR←⍉(⍺[2;1 2]-PE),⍪PE                       ⍝ Split Error into LOF, PE
         P←ERR[;1]fDist prob>F←÷/⍣2⊢ERR               ⍝ Calculate F and P
         Z←'  Lack of Fit' '  Pure Error',(⊢,÷/)ERR   ⍝ Insert Labels
         ⍺[⍳3;]⍪(Z,2 2⍴F P 0 0)⍪⍺[3;]}⍵               ⍝ Insert LOF into ANOVA Table
     T←B÷SB                                           ⍝ Test statistic
     P←2×DF tDist probability>|T                      ⍝ p-Value
     YX←y,⍉1↓X
     Hii←0 0⍉H←(⍉X)+.×C+.×X                           ⍝ Hat Matrix
     R←E÷S×sqrt 1-Hii                                 ⍝ Studentized Residuals
     CooksD←(R*2)÷(≢B)÷(⊢÷1∘-)Hii                     ⍝ Cook's Distance
     PRESS←sumSquares E÷1-Hii                         ⍝ PRESS Statistic
     ⍝VIF←0 0⍉⌹XX←corr int↓↓X                         ⍝ Variance inflation factor
     XX←corr int↓↓X
     VIF←{0::⌊/⍳0 ⋄ 0 0⍉⌹⍵}XX                         ⍝ Variance inflation factor
     ConditionNumber←(⌈/÷⌊/)eigenValues XX            ⍝ Condition Number
     PR←'B,T,P,N,XBAR,SSX,DF,S,MSE,SB,E,YHAT'         ⍝ Put additional
     PR,←',YX,C,VIF,R,H,ConditionNumber,CooksD,PRESS' ⍝
     _←'ns'⎕NS toNestedVector PR                      ⍝  properties into namespace
     ns
 }
