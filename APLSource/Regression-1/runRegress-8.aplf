 runRegress←{
     ⎕IO←0 ⋄ ⎕ML←3
     int←⍺ ⋄ W←⍵
     X←(~int)↓1⍪1↓⊃⍉W                                 ⍝ Predictor Variables
     N←≢y←↑W                                          ⍝ Sample Size
     C←⌹X+.×⍉X                                        ⍝ Covariance Matrix
     k←(¯1×int)+≢B←C+.×Xy←X+.×y                       ⍝ Estimate coefficients
     SSE←(y+.*2)-B+.×Xy                               ⍝ Error Sum of Squares
     E←y-YHAT←B+.×X                                   ⍝ Residuals
     ss←{(⍵+.*2)-⍺×N÷⍨(+/⍵)*2}                        ⍝ SumSquares function
     SST←int ss y                                     ⍝ Total sum of Squares
     SSX←1↓int ss X                                   ⍝ Why is this here?
     XBAR←mean¨W                                      ⍝ Means for each X
     DF←N-k+int                                       ⍝ Degrees of Freedom
     MSE←SSE÷DF                                       ⍝ Mean Square Error
     S←MSE*÷2                                        ⍝ Standard error for regression
     SB←(MSE×0 0⍉C)*÷2                                ⍝ Standard Error for coeffs
     SSR←SST-SSE                                      ⍝ Regression sum of squares
     ns←'Regression'buildAnovaTable SSR SSE,k DF,⍪2 0 ⍝ Create ANOVA table
     T←B÷SB                                           ⍝ Test statistic
     P←2×DF tDist probability>|T                      ⍝ p-Value
     YX←y,⍉1↓X
     H←(⍉X)+.×C+.×X                                   ⍝ Hat Matrix
     R←E÷S÷sqrt 1-0 0⍉H                              ⍝ Studentized Residuals
     VIF←0 0⍉⌹XX←corr int↓↓X                          ⍝ Variance inflation factor
     ConditionNumber←(⌈/÷⌊/)eigenValues XX            ⍝ Condition Number
     PR←'B,T,P,N,XBAR,SSX,DF,S,MSE,SB,E,YHAT,YX,C,VIF,R,H' ⍝ Put additional
     PR,←',ConditionNumber'
     _←'ns'⎕NS toNestedVector PR                      ⍝  properties into namespace
     ns
 }
