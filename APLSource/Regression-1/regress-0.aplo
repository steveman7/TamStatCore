 regress←{
 ⍝∇  Regress Operator
 ⍝⍟  Copyright 2023 by Stephen Mansour
 ⍝∊  Operator
 ⍝⍺  Y         Yv Respose Variable
 ⍝⍺⍺ YName     Cv Name of response variable
 ⍝⍺⍺ f         f  Transformation Function
 ⍝⍵  X         Xv Predictor Variable
 ⍝⍵  Xn        Xn List of Predictor Variables
 ⍝⍵  DataBase W  Contains Predictor and Response Variables
 ⍝← Model W Contains Coefficients, AnovaTable, R-Squared, predictor function f
 ⍝⍕ Model←Y[f]|transform|YName regress X|Xn|Database
 ⍝⍎ report Height regress ShoeSize              ⍝ Simple Regression
 ⍝⍎ report Weight regress Height ShoeSize       ⍝ Multiple Regression
 ⍝⍎ report Height regress ShoeSize (Sex eq 'M') ⍝ Indicator Variable
 ⍝⍎ report Height quad regress ShoeSize         ⍝ Quadratic regression
 ⍝⍎ report Weight cubic regress ShoeSize        ⍝ Cubic regression
 ⍝⍎ report Y ln regress X                       ⍝ Log transform
 ⍝⍎ report transform regress DataBase           ⍝ User-defined transformation
     ⍺←'' ⋄ ⎕IO←0 ⋄ ⎕ML←1
     _←⎕EX'#.∆NS'
     FN←⊃3=⎕NC'⍺⍺'                       ⍝ Is left operand a function?
     DB←⊃9=⎕NC'⍵'                        ⍝ Is right argument a database?
     Int X←{(1=≡⍵)∨~(⊂⊃⍵)∊0 1:1 ⍵ ⋄ 2=≢⍵:⍵
         (⊃⍵)(1↓⍵)}⍵                     ⍝ Include intercept?
     ⍝2=⍴⍴X:Int leastSquares ⍺⍺,X        ⍝ Matrix right argument?
     B←{9=⎕NC'⍵':0 ⋄ (2=⍴⍴⍵)∧0=⊃⊃0⍴⍵}X ⍝ If Matrix right argument
     B:Int leastSquares ⍺⍺,X            ⍝  and numeric
     ~FN:(,⍺⍺)(Int regressInd 1)X        ⍝ If not a function
     FN∧~DB:⍺ ⍺⍺{U←3 ⍺⍺ 4                ⍝ Test operand
         U=4:⍺(Int regressPoly)⍵         ⍝ Polynomial (⊥)
         U=5:⍺(⍺⍺ 1)(Int regressPoly)⍵   ⍝ Cubic/Quadratic
         U∊0 1:⍺(Int regressBin)⍵        ⍝ Binary regression (= or ≠)
         ⍺(⍺⍺ vst Int)⍵                  ⍝ Variance stabilizing transformation
     }X
     DB∧~FN:(,⍺⍺)(Int regressInd 1)⍵        ⍝ Don't Ignore Character Fields
     FN∧⍺≢'':⍺ ⍺⍺{
         X←⍵.(⍎⍕(' '~⍨¨↓⎕NL 2)~⊂⍺)          ⍝ Unpack predictor variables
         (⍵⍎⍺)(⍺⍺ regress)X            ⍝ Apply regress
       ⍝  U←2 ⍺⍺ 4               ⍝ Treat char fields as
⍝         U=4:⍺(Int regressPoly)⍵
⍝         U=5:⍺(⍺⍺ 1)(Int regressPoly)⍵  ⍝ Cubic/Quadratic
⍝         U∊0 1:⍺(Int regressBin)⍵
     }⍵
     FN∧⍺≡'':⍺⍺{NS←⍺⍺ ⍵                  ⍝ If user operand, apply transformation
         V←⊆variables NS                 ⍝ Get variable names
         V←⍕'Y',V~,¨'Y' 'Int'            ⍝ Put response variable first
         NS←NS.Int leastSquares NS V     ⍝ Do regression
         ⍵ ⍺⍺ buildNL NS}X               ⍝ Build non-linear function

     Y←,⍺⍺{2=⎕NC'⍺⍺':⍺⍺ ⋄ ⍵}⍺            ⍝ Get name of response variable
     V←⊆variables ⍵                      ⍝ Get transformation variable names
     V←⍕Y,V~'Int'Y                       ⍝ Put response variable first
     ⍵.∆←⍺⍺¨⍣FN⊢⍵⍎V                      ⍝ Apply transformation function to all variables
     _←⍵⍎V,'←∆'                          ⍝ Reassign transformed data function names
     _←⍵.(⎕EX'∆')                        ⍝ Clean up garbage
     I←{0::1 ⋄ ⊃⍵.Int}⍵                  ⍝ Include intercept?
     I leastSquares ⍵ V                  ⍝ Do regression
 }
