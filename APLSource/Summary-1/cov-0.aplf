 cov←{
 ⍝⍟ Copyright 2018 by Stephen M. Mansour
 ⍝∇ Covariance
 ⍝∊ Summary Function (Dyadic); Measure of Association
 ⍝⍺ [Y]          Yv  Raw Data
 ⍝⍺ [Database]   W   Namespace containing Variables
 ⍝⍵ X            Xv  Raw Data
 ⍝⍵ NameList     Cv  List of Variable Names
 ⍝⍵ XX           Xv  X1 X2 ... Xn
 ⍝⍵ Database     W   Namespace
 ⍝← Value   Xs  Sample Covariance
 ⍝← Matrix  Xm  Covariance Matrix
 ⍝← Table   Cm  Covariance Table with Labels
 ⍝⍕ [Y|Database] cov X|XX|NameList|Database
 ⍝⍎ Value←Y cov X                      ⍝ Produces Sample Covariance
 ⍝⍎ Matrix←cov X1 X2 ... Xn            ⍝ Produces Covariance Matrix
 ⍝⍎ Table←Database cov 'X1 X2 ... Xn'  ⍝ Produces Covariance Table for X1 ... Xn
 ⍝⍎ Matrix←cov Database.(X1 X2 ... Xn) ⍝ Produces Covariance Matrix
 ⍝⍎ Table←cov Database                 ⍝ Produces Covariance Table for all numeric variables
 ⍝⍎ Height cov Weight
 ⍝⍎ cov Height Weight ShoeSize
     ⎕IO←0 ⋄ ⎕ML←3
    ⍝ 0=⎕NC'⍺':∘.∇⍨⍵                   ⍝ No left arg, make cov matrix
     0=⎕NC'⍺':∇{9≠⎕NC'⍵':∘.⍺⍺⍨⍵
         ⍵ ⍺⍺⍕2 variables ⍵}⍵
     9=⎕NC'⍺':⍕⍺ ∇{m←⍺⍺ ⍺.(⍎⍵)         ⍝ If namespace
         m{(⍵,⍨⊂'cov'),⍵⍪⍺}(⍵≠' ')⊂⍵}⍵ ⍝ Add labels
     0J1∊⍺:(1↓⍺)showCov ⍵              ⍝ Display covariance
     1∊≢¨⍺ ⍵:0                         ⍝ 0 if scalar
     rAv:+##.∆r.x'cov(⍵,⍵)'⍺ ⍵         ⍝ R calculation
     n←≢⍵                              ⍝ Sample Size
     z←(n×⍺+.×⍵)-×/+/⊃⍺ ⍵              ⍝ Sum of Squares
     z÷n×n-1                           ⍝ Covariance
 }
