 quartile←{
     ⍝⍟ Copyright (c) 2018, by Stephen Mansur
     ⍝∇ Find the Nth Quartile of a data set
     ⍝∊ Summary Function; Measure of Position
     ⍝⍺ [N]         Iv  Quartile (0=min,2=Median,4=max),omitted = All quartiles
     ⍝⍵ RawData     Xv  Sample or Population Data
     ⍝⍵ FreqDist    Xm2 Values followed by Counts or Probabilities
     ⍝← Result      Zs  Numeric Scalar
     ⍝⍕ Result ←  [0|1|2|3|4] quartile RawData|FreqDist
     ⍝⍎ 32 ← 1 quartile 32 58 57 96 76 87 21 58 90 5  ⍝ First Quartile (Q1)
     ⍝⍎ 87 ← 3 quartile 32 58 57 96 76 87 21 58 90 5  ⍝ Third Quartile (Q3)
     ⍝⍎ 5 32 58 87 96 ← quartile 32 58 57 96 76 87 21 58 90 5 ⍝ All quartiles
     ⎕ML←3 ⋄ ⎕IO←0
     ⍺←⍳5
     showQuartile←{
        ⍝ showMedian¨showMedian ⍵
         l c r←showMedian ⍵
         (showMedian l)c(showMedian r)
     }
     showMedian←{
         0::⍵
         n←≢d←⍵[⍋⍵]   ⍝ Sample Size
         odd←2∘|      ⍝ Odd or Even?
         c←⌈(n-1)÷2   ⍝ Center of the data
         odd n:(c↑d)('(',d[c],')')((-c)↑d)
         (c↑d)('|')(c↓d)
     }
     0J1∊⍺:showQuartile ⍵~⎕NULL
     1=≡⍺:⍺ ∇¨⊂⍵
     ⎕NULL∊⍵:⍺∘quartile tolerant ⍵
     ⍺=0:↑⌊⌿⍵
     ⍺=4:↑⌈⌿⍵
     m←median ⍵
     ⍺=2:m
     2=⍴⍴⍵:(25×⍺)percentile ⍵
     n←≢x←⍵[⍋⍵]
     ⍺=1:median(⌊n÷2)↑x
     ⍺=3:median(⌈n÷2)↓x
     ⎕SIGNAL 11
 }
