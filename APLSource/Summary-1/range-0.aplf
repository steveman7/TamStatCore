 range←{
     ⍝⍟ Copyright (c) 2024 by Stephen Mansur
     ⍝∇ Calculate range of a data set or frequency distribution
     ⍝∊ Summary Function; Measure of Spread
     ⍝⍵ RawData     Xv  Sample or Population Data
     ⍝⍵ FreqDist    Xm2 Values followed by Counts or Probabilities
     ⍝⍵ SummaryData W   Namespace containing count, mean, sdev
     ⍝← Diff Ts  Difference between largest and smallest value
     ⍝⍕ Diff ← range RawData|FreqDist|SummaryData
     ⍝⍎ 7 ← range 2 8 5 1 3                        ⍝ Raw Data
     ⍝⍎ 3 ← range 4 2 matrix 0 20 1 30 2 40 3 10   ⍝ Frequency Distribution
     9=⎕NC'⍵':∇{2=⍵.⎕NC'events':~⍵.(events∊0,count)⍝ If bernoulli, 1 or 0
         ⍺⍺ ⍵.(mean sdev)normal randVar ⍵.count}⍵  ⍝ Else assume normal
     (1<⍴⍴⍵)∧~isFrequency ⍵:'Rank Error'           ⍝ If not frequency
     ⍝(⌈/-⌊/)(⍪⍵)[;⎕IO]~⎕NULL}                     ⍝
     (⌈/-⌊/){(⍵∧.≠⎕NULL,0)/⊣/⍵}(1,⍨⍪⍵)[;⍳2]        ⍝ Remove nulls, quan = 0
 }
