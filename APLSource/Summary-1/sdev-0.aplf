 sdev←{
 ⍝⍟ Copyright 2017 by Stephen M. Mansour
 ⍝∇ Standard Deviation
 ⍝∊ Summary Function; Measure of Spread
 ⍝⍺_ 0j1 = Show calculations
 ⍝⍺ [Population]  Bs 1=population, 0=sample, Omitted: Determine from data
 ⍝⍵ DataSet     Xv  Sample or Population Data
 ⍝⍵ FreqDist    Xm2 Values followed by Counts or Probabilities
 ⍝⍵ SummaryData W   Namespace containing count, mean, sdev
 ⍝← Result      Ss  Variance or unalikeability if character
 ⍝⍕ Result ← [0|1|_¯1] sdev DataSet|FreqDist|SummaryData
 ⍝⍎ 2.775 ← sdev 2 8 5 1 3      ⍝ Raw Data
 ⍝⍎ 0.92113 ← sdev 4 2 matrix 0 20 1 30 2 40 3 10 ⍝ Frequency Distribution
 ⍝⍎ 0.91652 ← sdev 4 2 matrix 0 0.2 1 0.3 2 0.4 3 0.1 ⍝ Relative Frequency
     ⍺←¯1 ⋄ ⎕IO←0 ⋄ ⎕ML←1
     9=⎕NC'⍵':⍵.sdev×sqrt 1-(⍺=1)÷⍵.count
     ⍵≡'Type':'Spread'
     ⊃⍺ between 0 1:sqrt ⍺ confVar ⍵
     rAv:⍺{z←##.∆r.x'sd(⍵)'(⍵~⎕NULL)
         z≡⎕NULL:0
         ⍺=1:z×sqrt÷/¯1 0+≢⍵ ⋄ z}⍵
     ⍝(⍺ var ⍵)*÷2}
     ⎕NULL≡z←⍺ var ⍵:z
     z*÷2}
