 max←{
     ⍝⍟ Copyright (c) 2018, by Stephen Mansur
     ⍝∇ Find maximum of a data set or frequency distribution
     ⍝∊ Summary Function
     ⍝⍵ Numeric Vector: Data Set; 2-col Numeric Matrix:  Frequency Dist
     ⍝← Numeric Scalar
     ⍝⍕ Zs ← max Xv    ⍝ Monadic
     ⍝⍕ Zs← X max Y    ⍝ Dyadic - smaller of two values
     ⍝⍎ 8 ← max 2 8 5 1 3
     ⍝⍎ 6 3 3 ← 3 max 6 1 3
     ⎕ML←3 ⋄ ⎕IO←0 ⋄ ⍺←⍬              ⍝ Set system variables
     ⍺≢⍬:⍺{~⎕NULL∊⍺,⍵:⍺⌈⍵
         b←∧/⎕NULL≠yx←⍺,⍪⍵          ⍝ Dyadic version
         (⎕NULL@((~b)/⍳⍴b))b\⌈/b⌿yx}⍵ ⍝
     9=⎕NC'⍵':{n←⍵.count            ⍝ Get count
         z←normal criticalValue>n÷n+1 ⍝ Estimate z-score
         ⍵.mean+z×⍵.sdev}⍵            ⍝ Estimate max
   ⍝  m←↑⌈⌿⍵                           ⍝ Get maximum
     b←∧/⎕NULL≠⍪⍵
     m←↑⌈⌿b⌿⍵                         ⍝ Exclude nuls
     1=⍴⍴⍵:m                          ⍝ If data Set, done
     ~isFrequency ⍵:'Rank Error'
     ⍝i←∪2-/⍵[⍋⍵;0]                    ⍝ Else get interval
     i←∪2-/{⍵[⍋⍵;0]}b⌿⍵               ⍝ Else get interval
     1<≢i:m                           ⍝ If irregular, done
     m-⌈i÷2                           ⍝ Else get upper boundary
 }
