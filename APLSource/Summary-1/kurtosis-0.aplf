 kurtosis←{
 ⍝⍟ Copyright 2018, 2024 by Stephen Mansour
 ⍝∇ Excess Kurtosis (Thickness of the tails)
 ⍝∊ Summary Function; Measure of Shape
 ⍝⍵ RawData     Xv  Sample or Population Data
 ⍝⍵ FreqDist    Xm2 Values followed by Counts or Probabilities
 ⍝← Z           Zs  Z>0 Leptokurtic; Z=0 Mesokurtic; Z<0 PlatykurticLeft
 ⍝⍕ Zs ← kurtosis Rawdata|FreqDist
 ⍝⍎ ¯0.1518 ← kurtosis  3 4 5 2 3 4 5 6 4 7   ⍝ Sample
     ⎕DIV←1 ⋄ ⎕ML←3 ⋄ ⎕IO←0     ⍝ Set system variables
     9=⎕NC'⍵':'Undefined for stats'   ⍝ Undefined
   ⍝  x←{isFrequency ⍵:∊(//⌽⍵) ⋄ ⍵}⍵    ⍝ If frequency, replicate values
     w←⍵⌿⍨⎕NULL≠⊣/⍪⍵                  ⍝ Remove nulls
     n m s←(count,mean,sdev)w
     4>n:⎕NULL                        ⍝ Sample size too small
     d4←{~isFrequency ⍵:+/(⍵-m)*4
         ⍵[;1]+.×(⍵[;0]-m)*4 ⋄ }w
     ⍝ rAv:+#.∆r.x'kurtosis(⍵)'w       ⍝ If requested, call R
    ⍝ z←+/(s÷⍨x-m)*4                   ⍝ Get z-score
     z←(+/d4)÷s*4
     z←z×(n×n+1)÷n×.-1 2 3            ⍝
     z-3×(n-1 2 3)×.*2 ¯1 ¯1          ⍝ Calculate kurtosis


 }
