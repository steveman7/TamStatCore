 iqr←{
 ⍝⍟ Copyright (c) 2018, 2024 by Stephen Mansur
 ⍝∇ Interquarile range  |Q3-Q1|
 ⍝∊ Summary Function; Measure of Spread
 ⍝⍵ DataSet     Xv  Sample of Population Data
 ⍝⍵ FreqDist    Xm2 Values followed by Counts or Probabilities
 ⍝⍵ SummaryData W   Namespace containing count, mean, sdev
 ⍝← Diff Ts  Difference between third and first quartiles
 ⍝⍕ Diff ← iqr DataSet|FreqDist|SummaryData
 ⍝⍎ 5 ← iqr 2 8 5 1 3                              ⍝ Raw Data
 ⍝⍎ 1 ← iqr 4 2 matrix 0 20 1 30 2 40 3 10         ⍝ Frequency Distribution
     9=⎕NC'⍵':-/{P←0.75 0.25
         2=⍵.⎕NC'events':⍵.events<P×⍵.count
         ⍵.(mean sdev)normal critVal>P}⍵        ⍝ Normal or
     (1<⍴⍴⍵)∧~isFrequency ⍵:'Rank Error'           ⍝ If not frequency
  ⍝   rAv:+##.∆r.x'IQR(⍵)'⍵                         ⍝ Call R if requested
     rAv:'IQR'rExec ⍵
     -/3 1 quartile ⍵   ⍝ Q3 - Q1                  ⍝ Else difference between Q3 and Q1
 }
