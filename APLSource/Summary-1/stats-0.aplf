 stats←{
     ⍝⍟ Copyright (c) 2018, by Stephen Mansour
     ⍝∇ Create summary stats object (count,mean,sdev)
     ⍝∊ Summary Functions
     ⍝⍺ Name Cv
     ⍝⍵ Vector2 Nv    Numeric 2- vector:   Count, Events|Prob   (Events if integer, prob if between 0 and 1)
     ⍝⍵ Vector3 Nv    count, mean, sdev
     ⍝⍵ VectorN Nv    Data set
     ⍝⍵ Frequency Distribution:  2-column matrix
     ⍝⍵ Namespace W   Stats object
     ⍝← Stats object
     ⍝⍕ NS ← ['Name'] stats  Count Events|(Mean Sdev)
     ⍝⍎ stats 1000 646        ⍝ N=1000, Sucesses=626
     ⍝⍎ stats 1000 0.4        ⍝ N=1000, P=0.4
     ⍝⍎ stats 30 10.5 2.7     ⍝ N=30, mean=10.5 sdev=2.7
     ⍝⍎ 'Height' stats 30 68 3.2
     ⍝⍎ stats Height          ⍝ Raw Data
     ⎕IO←0 ⋄ ⎕ML←3 ⋄ ⍺←'' ⋄ ⎕PP←5                ⍝ Set system variables
    ⍝ 326≡⎕DR ⍵:∇{∆←⍺⍺ 1↓⍵ ⋄ ∆.Name←↑⍵ ⋄ ∆}⍵ ⍝ Insert name if 1st item Char
     b←0=≢⍺
     name←b⊃⍺'Namespace'
     isFrequency ⍵:∇{⍵[;0]≡1 0:⌽+\⍵[;1]          ⍝ If boolean
         (count,mean,sdev)⍵}⍵[⍒⍵;]          ⍝ If frequency
     2≤⍴⍴⍵:'Rank Error'                          ⍝ Must be vector or namespace
     3=≢⍵:name{count mean sdev←3↑⍵               ⍝ If 3-vector
         var←sdev*2 ⋄ name←⍺                     ⍝ Calculate variance
         NS←⎕NS b↓'name' 'count' 'mean' 'sdev' 'var'
         DF←⍺,': [n=',(⍕count),'; xbar=',(⍕mean),'; s=',(⍕sdev),']'
         _←NS.⎕DF DF ⋄ NS}⍵   ⍝ Include count,mean,sdev
     2=≢⍵:name{count←↑⍵                          ⍝ If 2-vector
         mean←↑{¯1=×/×⍵-0 1:⍵                    ⍝ Assume binomial
             ⍝(⍵>0)∧(⍵=⌈⍵)∧⍵≤⍺:⍵÷⍺ ⋄ ¯1}/⍵       ⍝ Calc mean
             (⍵≥0)∧(⍵=⌈⍵)∧⍵≤⍺:⍵÷⍺ ⋄ ¯1}/⍵        ⍝ Calc mean
         var←{⍵×1-⍵}mean ⋄ name←⍺                ⍝ Var = p(1-p)
         events←⌊0.5+count×mean                  ⍝ Successes
         sdev←var*÷2                             ⍝ sdev= sqrt var
         NS←⎕NS b↓'name' 'count' 'events' 'mean' 'sdev' 'var'
         DF←⍺,': [n=',(⍕count),'; x=',(⍕events),']'
         _←NS.⎕DF DF ⋄ NS}⍵                    ⍝
     6=≢⍵:⍺{'Nonce Error'}⍵
     9=⎕NC'⍵':⍵                                  ⍝ If ns leave alone
     count mean var sdev←(count,mean,var,sdev)⍵  ⍝ Put variables
    ⍝ name←⍺
     NS←⎕NS b↓'name' 'count' 'mean' 'sdev' 'var' ⍝   into namespace
     DF←name,': [n=',(⍕count),'; xbar=',(⍕mean),'; s=',(⍕sdev),']'
     _←NS.⎕DF DF ⋄ ~∧/⍵∊0 1:NS ⋄
     NS.events←+/⍵ ⋄ DF←name,': [n=',(⍕count),'; x=',(⍕NS.events),']'
     _←NS.⎕DF DF ⋄ NS                            ⍝ Include events if binary
 }
