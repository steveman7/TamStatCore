 stats←{
     ⍝⍟ Copyright (c) 2018, by Stephen Mansur
     ⍝∇ Create summary stats object (count,mean,sdev)
     ⍝∊ Summary Functions
     ⍝⍵ Numeric 2- vector:   Count, Events|Prob
     ⍝⍵       Events if integer, prob if between 0 and 1
     ⍝⍵ Numeric 3-Vector:    count, mean, sdev
     ⍝⍵ Numeric n-Vector:    Data set
     ⍝⍵ Namespace:           Stats object
     ⍝← Stats object
     ⍝⍕ NS ← ['Name'] stats  Count Events|(Mean Sdev)
     ⍝⍎ stats 1000 646
     ⍝⍎ stats 30 10.5 2.7
     ⍝⍎ stats 'Height' 30 68 3.2
     ⍝⍎ stats Height
     ⎕IO←0 ⋄ ⎕ML←3 ⋄ ⍺←''                   ⍝ Set system variables
    ⍝ 326≡⎕DR ⍵:∇{∆←⍺⍺ 1↓⍵ ⋄ ∆.Name←↑⍵ ⋄ ∆}⍵ ⍝ Insert name if 1st item Char
     b←0=≢⍺
     3=≢⍵:⍺{count mean sdev←3↑⍵             ⍝ If 3-vector
         var←sdev*2 ⋄ name←⍺               ⍝ Calculate variance
         ⎕NS b↓'name' 'count' 'mean' 'sdev' 'var'}⍵   ⍝ Include count,mean,sdev
     2=≢⍵:⍺{count←↑⍵                        ⍝ If 2-vector
         mean←↑{¯1=×/×⍵-0 1:⍵               ⍝ Assume binomial
             ⍝(⍵>0)∧(⍵=⌈⍵)∧⍵≤⍺:⍵÷⍺ ⋄ ¯1}/⍵   ⍝ Calc mean
             (⍵≥0)∧(⍵=⌈⍵)∧⍵≤⍺:⍵÷⍺ ⋄ ¯1}/⍵   ⍝ Calc mean
         var←{⍵×1-⍵}mean ⋄ name←⍺           ⍝ Var = p(1-p)
         events←⌊0.5+count×mean             ⍝ Successes
         sdev←var*÷2                        ⍝ sdev= sqrt var
         ⎕NS b↓'name' 'count' 'events' 'mean' 'sdev' 'var'}⍵ ⍝
     6=≢⍵:⍺{'Nonce Error'}⍵
     9=⎕NC'⍵':⍵                             ⍝ If ns leave alone
     count mean var sdev←(count,mean,var,sdev)⍵     ⍝ Put variables
     name←⍺
     ⎕NS b↓'name' 'count' 'mean' 'sdev' 'var'         ⍝   into namespace

 }
