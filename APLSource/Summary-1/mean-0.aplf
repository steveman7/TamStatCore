 mean←{
     ⍝⍟ Copyright (c) 2017, by Stephen Mansur
     ⍝∇ Calculate mean of a data set or frequency distribution
     ⍝∊ Summary Function
     ⍝⍺ 0j1 = Show calculations; 0 = only show answer
     ⍝⍵ Numeric Vector: Data Set; 2-col Numeric Matrix:  Frequency Dist
     ⍝← Numeric Scalar
     ⍝⍕ Ys ← mean Xv
     ⍝⍎ 3.8 ← mean 2 8 5 1 3
     ⎕ML←3 ⋄ ⎕IO←0 ⋄ ⎕DIV←1 ⋄ ⍺←0      ⍝ Set up variables
     ⍵≡'Type':'Center'                 ⍝ Measure of center
     9=⎕NC'⍵':⍵.mean                   ⍝ If namespace, get mean property
   ⍝  ~'X'check ⍵:⎕SIGNAL 11            ⍝
     w←frequency⍣(↑(0J1=⍺)∧1=⍴⍴⍵)⊢⍵    ⍝ Convert to frequency?
    ⍝ 2=⍴⍴w:⍺{⍺=0:(+/×/⍵)÷+/⍵[;1]       ⍝ If matrix, treat as frequency
     isFrequency w:⍺{⍺=0:(+/×/⍵)÷+/⍵[;1] ⍝ If matrix, treat as frequency
         d←⍵[;,0],(⍵[;1]÷+/⍵[;1])        ⍝ Values, frequencies
         d←d,×/d ⋄                       ⍝ X×p
         FS←'I10,F10.5,F10.5'          ⍝ Set widths to 10
         z←FS ⎕FMT d⍪+⌿d               ⍝ Format data and totals
         h←∊¯10↑¨'x' 'P(x)' 'xP(x)'    ⍝ Build Heading
         u←30⍴1 9/' -'                 ⍝ Underline heading
         (h⍪u⍪(¯1↓z)⍪u)⍪¯30↑'Total:',10↓,¯1↑z ⍝ Assemble table
     }w
     2=≡w:∇¨w~¨⎕NULL                   ⍝ Nested?
     rAv:+##.∆r.x'mean(⍵)'(⍵~⎕NULL)    ⍝ If available, use R
     (+/÷≢)w~⎕NULL                     ⍝ Use fork to calculate mean
 }
