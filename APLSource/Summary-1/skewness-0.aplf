 skewness←{
 ⍝⍟ Copyright 2017, 2024 by Stephen Mansour
 ⍝∇ Sample Skewness https://www.itl.nist.gov/div898/handbook/eda/section3/eda35b.htm
 ⍝∊ Summary Function; Measure of Shape
 ⍝⍺ [Population]  Bs 1=population, 0=sample, Omitted: Determine from data
 ⍝⍵ RawData     Xv  Sample or Population Data
 ⍝⍵ FreqDist    Xm2 Values followed by Counts or Probabilities
 ⍝⍵ SummaryData W   Namespace containing count, mean, sdev
 ⍝← Z           Zs  Z>0 Skewed right; Z=0 Symmetric; Z<0 Skewed Left
 ⍝⍕ Zs ← skewness Rawdata|FreqDist
 ⍝⍎ 0.35954 ← skewness  3 4 5 2 3 4 5 6 4 7   ⍝ Sample
 ⍝⍎ 0.30319 ← 1 skewness  3 4 5 2 3 4 5 6 4 7 ⍝ Population
     ⎕DIV←1 ⋄ ⎕ML←3 ⋄ ⎕IO←0 ⋄ ⍺←¯1 ⍝ Set system variables
     ⍝ rAv:+##.∆r.x'skewness(⍵)'⍵  ⍝ If requested call R
     n m s←(count,mean,1∘sdev)⍵    ⍝ Get count, mean, sdev of data
     9=⎕NC'⍵':'Nonce Error'
    ⍝ x←{isFrequency ⍵:∊(//⌽⍵) ⋄ ⍵}⍵
     isFrequency ⍵:⍺{U←+/⍵[;1]×3*⍨⍵[;0]-m
         1∨.=⍺,+/⍵[;1]:U÷(+/⍵[;1])×s*3  ⍝ Population
         (U÷n)×(sqrt n×n-1)÷(n-2)×s*3}⍵ ⍝ Sample                        ⍝ Sample
    ⍝ z←+/(s÷⍨⍵-m)*3
     3>n←≢⍵:'Sample Size Too Small' ⍝ If Sample size < 3, get out
     z←-n÷⍨+/(s÷⍨m-⍵~⎕NULL)*3
     ⍺=1:z
     z×(sqrt n×n-1)÷n-2
 }
