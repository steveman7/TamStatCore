 product←{ ⍝⍟ Copyright (c) 2018, by Stephen Mansur
     ⍝∇ Calculate sample product of a data set or frequency distribution
     ⍝∊ Summary Function; Measure of Quantity
     ⍝⍵ DataSet Xv Sample of Population Data
     ⍝⍵ FrequencyDistribution Nm  2-column matrix: Values Counts
     ⍝⍵ SummaryData W Namespace containing count, mean, sdev
     ⍝← Product Xs   Numeric Scalar
     ⍝⍕ Ys ← product Xv|Xm|W
     ⍝⍎ 240 ← product 2 8 5 1 3
     11::(×/×⍵)×⌊/⍳0         ⍝ Represent infinity if domain error
     9=⎕NC'⍵':(mean*count)   ⍝ Use Geometric mean
     w←(∧/⎕NULL≠⍪⍵)⌿⍵
     isFrequency w:×.*/↓⍉w   ⍝ Frequency Distribution
     ×/w                     ⍝ Raw Data
 }
