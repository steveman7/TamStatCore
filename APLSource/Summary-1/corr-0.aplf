 corr←{⎕IO←0 ⋄ ⎕ML←3 ⋄ ⎕DIV←1
 ⍝⍟ Copyright 2018, 2025 by Stephen M. Mansour
 ⍝∇ Correlation
 ⍝∊ Summary Function (Dyadic); Measure of Association
 ⍝⍺ [Y]          Yv  Raw Data
 ⍝⍺ [Database]   W   Namespace containing Variables
 ⍝⍵ X            Xv  Raw Data
 ⍝⍵ NameList     Cv  List of Variable Names
 ⍝⍵ XX           Xv  X1 X2 ... Xn
 ⍝⍵ Database     W   Namespace
 ⍝← Value   Xs  Sample Covariance
 ⍝← Matrix  Xm  Correlation Matrix
 ⍝← Table   Cm  Correlation Table with Labels
 ⍝⍕ [Y|Database] corr X|XX|NameList|Database
 ⍝⍎ Value←Y corr X                      ⍝ Produces Sample Correlation Coefficient
 ⍝⍎ Matrix←corr X1 X2 ... Xn            ⍝ Produces Correlation Matrix
 ⍝⍎ Matrix←corr Database.(X1 X2 ... Xn) ⍝ Produces Correlation Matrix
 ⍝⍎ Table←corr Database                 ⍝ Produces Correlation Table for all numeric variables
 ⍝⍎ Table←'X1 X2 ... Xn' corr Database  ⍝ Produces Correlation Table for X1 ... Xn
 ⍝⍎ Height corr Weight
 ⍝⍎ corr Height Weight ShoeSize
     ⍵≡'Type':'Association'             ⍝ Bivariate function
     0=⎕NC'⍺':∇{
         0=≡⍵:⍵ ⍺⍺⍨⍕2 variables ⍵       ⍝ If namespace make corr table with labels
         1=≡⍵:↑⍺⍺/2 toNestedVector ⍵    ⍝ If simple vector split into two
         2=≡⍵:∘.⍺⍺⍨⍵}⍵                  ⍝ If nested vector make corr matrix
     (9=⎕NC'⍵'):⍕⍵ ∇{m←⍺⍺ ⍺⍎⍵           ⍝ If namespace
         m{(⍵,⍨⊂'corr'),⍵⍪⍺}(⍵≠' ')⊂⍵}⍺ ⍝ Add labels
     C Y X←⍺{1<≢⍺:0 ⍺ ⍵                 ⍝ Confidence Level
         ⍺,2 toNestedVector ⍵}⍵         ⍝ Dependent and Independent Vars
     B←∧/⎕NULL≠Y,⍪X                     ⍝ Check for missing data
     Y X←B∘/¨Y X                        ⍝ Remove incomplete data
   ⍝ rAv∧C=0:+##.∆r.x'cor(⍵,⍵)'Y X      ⍝ Let R do it
     rAv∧C=0:'cor'rExec Y X            ⍝ Let R do it
     M←mean¨Y X ⋄ S←sdev¨Y X            ⍝ Find means and stdev's
     R←(+/×⌿⊃Y X-M)÷×/S,¯1+≢X           ⍝ Point estimate of correlation
     C=0:R                              ⍝ If confidence interval
     Z←normal critVal<0.5×1-C           ⍝ Get normal distribution
     7○(¯7○R)(-,+)Z÷sqrt ¯3+≢X          ⍝ Montgomery p. 49 (arctanh R)
 }
