 proportion←{
     ⍝⍟ Copyright 2018 by Stephen M. Mansour
     ⍝∇ Sample proportion
     ⍝∊ Summary function
     ⍝⍵ Boolean Vector
     ⍝← Proportion 0≤z≤1
     ⍝⍎ .4 ← proportion 1 0 1 0 0
     ⍝⍎ proportion STATE eq 'PA'
     ⍝⍎ proportion Height > 72
     ⎕IO←0 ⋄ ⎕ML←3
     9=⎕NC'⍵':{1≠×/×⍵-0 1:⍵ ⋄ ¯1}⍵.mean  ⍝ Get mean if summary
     2=⍴⍴⍵:{b←∧/(⍵[;0]∊0 1)              ⍝ 1st col boolean?
         b←b∧(⍵[;1]≡⌊⍵[;1])∧⍵[;1]∧.≥0    ⍝ 2nd col pos integer?
         b:(+/×/⍵)÷+/⍵[;1] ⋄ ¯1}⍵        ⍝ Frequency dist
     ~∧/⍵∊0 1:¯1 ⋄ (+/⍵)÷↑⍴⍵             ⍝ Boolean vector
 }
