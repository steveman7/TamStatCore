 table←{
⍝⍺   Parameters or left argument to function (optional)
⍝⍺⍺  Function or Distribution expression, e.g (normal prob <=)
⍝⍵   Right argument to function
⍝    20 (.1 * iota 10) Binomial prob table = iota 20
⍝    normal  prob table <=  0 +.01 * iota 100
⍝    1 2 3 tDist criticalValue < table  .05  .025 .1
⍝    fDist criticalValue  table < .05
⍝   chiSquare criticalValue table > .01 .025 .05 .1 .9 .95 .975
     ⎕IO←0 ⋄ ⎕ML←3
     la←×⎕NC'⍺'                        ⍝ Check for left argument
     x←⍵
     la<(2=⍴⍴⍵):⍺⍺ x
     ⍺←⍬
     (1∧.<≢¨⍺ ⍵)∧1∧.=≡¨⍺ ⍵:⍺∘.⍺⍺ ⍵     ⍝ If left and right vectors make table
     (1<≢⍺)∧2=≡⍺:⊃(↑∘.,/⍺)⍺⍺¨⊂x        ⍝ If scalar right argument, vary the parameters
     ⍝⍝⍝⍝⍝⍝⍝⍝⍝ New Code ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
     b←(1 1 1≡×,≢,⊢=⌊)⍵                ⍝ If positive integer scalar
     w←b{⍺:1+⍳⍵ ⋄ ⍵}⍵                  ⍝ Make index generator
     ⍺≡⍬:∘.⍺⍺⍨w                        ⍝ If no left argument, generate a selfie
 }
