 variables←{
 ⍝⍟ Copyright (C) 2018 by Stephen M. Mansour
 ⍝∇ List variables in a namespace
 ⍝∊ Utils
 ⍝⍺ B:  0=List top level variables, 1=list all variables
 ⍝⍵ NS:  Namespace
 ⍝← C:  List of variable names
 ⍝⍕ C ←  variables [#|NS]            ⍝ All variables in root
 ⍝⍎ A B [C] ← variables #            ⍝ Root has 2 vars and 1 NS
 ⍝⍎ A B C.U C.V C.W ← 1 variables #  ⍝ 3 addt'l vars in C
 ⍝⍎ U V W ← 1 variables C            ⍝ List only vars in C
 ⍝⍎ variables D                      ⍝
  ⍝   0::'Domain Error'               ⍝
     ⍺←0 ⋄ ⎕IO←0 ⋄ ⎕ML←3             ⍝ Default is top-level
     NS←{''≡⍵~' ':# ⋄ 2=⎕NC'⍵':⍎⍵ ⋄ ⍵}⍵ ⍝ Get namespace
     V DB←toNestedVector∘NS.⎕NL¨2 9  ⍝ Find vars, ns
     V←('_'≠↑¨V)/V                   ⍝ remove meta-variables
     ⍝DB←DB~⊂'TamStat'
     DB←DB~'TamStat' '_meta'         ⍝ Exclude TamStat namespace
     (⍺=0)∨0=≢DB:V,{'[',⍵,']'}¨DB    ⍝ If 0 or no ns, list vars
    ⍝ VV←∇∘⍎¨'#.'∘,¨DB                ⍝ Else apply to each namespace
     VV←∇∘⍎¨'⍵.'∘,¨DB
     V,↑,/DB{⍺∘,¨'.',¨⍵}¨VV          ⍝ Append to list
 }
