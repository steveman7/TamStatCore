 tableLookup←{
⍝∇ Table lookup with interpolation
⍝⍟ (c) 2021 by Stephen Mansour
⍝⍺ Row Index
⍝⍵ Column Index
⍝⍺⍺ Table
⍝← Item corresponding to row and column
⍝← If index is between row and/or column, interpolate
     ⎕IO←0 ⋄ ⎕ML←3
     1<⍺⌈⍥≢⍵:⍺ ∇¨⍵
     f0←{RT←⍺⍺[;0] ⋄ CT←⍺⍺[0;]
         I←RT⍳⍺ ⋄ J←1⌈CT⍳⍵
         ⍺⍺[I;J]}
     f1←{RT←⍺⍺[;0] ⋄ CT←⍺⍺[0;]         ⍝ Row, Column Titles
         I←RT⍳⍺ ⋄ J←CT⍸⍵
         ⍝Y←⍺⍺[I;J+0 1]
⍝         X←⍺⍺[0;J+0 1]
         Y X←↓⍺⍺{I J←⍵
             J<¯1+↑⌽⍴⍺:⍺[I 0;J+0 1]
             +\-\⍺[I 0;J-0 1]}I J             ⍝  Project outward
       ⍝   ⌊Y[0]+(⍵-↑X)×Y÷⍥(-/)X
         Y[0]+(⍵-↑X)×Y÷⍥(-/)X
     }
     f2←{RT←⍺⍺[;0] ⋄ CT←⍺⍺[0;]         ⍝ Row, Column Titles
         I J←0 1∘+¨RT CT⍸¨⍺ ⍵          ⍝ Row, Column Indices
         Z←⍺⍺[I;J]                     ⍝ Extract data
         X←RT[I] ⋄ Y←CT[J]
         XY←X[0 0 1 1],⍪Y[0 1 0 1]
         A←(,Z)⌹1,XY,×/XY
         A+.×1 ⍺ ⍵,⍺×⍵}
     (⍺∊⍺⍺[;0])∧⍵∊⍺⍺[0;]:⍺(⍺⍺ f0)⍵
     ⍺∊⍺⍺[;0]:⍺(⍺⍺ f1)⍵
     ⍺(⍺⍺ f2)⍵
 }
