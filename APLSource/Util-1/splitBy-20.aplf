 splitBy←{
 ⍝⍟ Copyright (c) 2018, by Stephen Mansour
 ⍝∊ Selection Function
 ⍝∇ Split a variable into two groups.
 ⍝⍺ Variable Xv Any Vector or vector representation
 ⍝⍵ BinaryVector    Bv Any Vector or vector representation whose length is the same as ⍺
 ⍝⍵ SelectionVector Ev If ⍵ is not Boolean, create Boolean in relation to first element
 ⍝← Result Ev 2-item vector
 ⍝⍕ Result←Vector splitBy BinaryVector|SelectionVector
 ⍝⍎  Male Female ← Height splitBy Sex eq 'M'
 ⍝⍎  Female Male ← Height splitBy Sex eq 'F'
 ⍝⍎  X Y ← Height splitBy Sex
     ⎕IO←0 ⋄ ⎕ML←3
     a←toNestedVector ⍺           ⍝ Convert left argument to vector
     ∧/,⍵∊0 1:/∘a¨⍵(~⍵)           ⍝ If right arg boolean, divide into two groups
     w←{⍵∊⍵[0]}toNestedVector ⍵   ⍝ Else make first element of ⍵ is "success"
     /∘a¨w(~w)                    ⍝ Then divide into two groups
 }
