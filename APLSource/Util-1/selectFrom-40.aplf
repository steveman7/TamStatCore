 selectFrom←{
⍝⍟ Copyright (c) 2024 by Stephen Mansour
⍝∇ Select columns from a database
⍝∊ Database Function
⍝⍺ NameList Cv comma-delimited list of variable/column names
⍝⍵ Database W  Namespace containing Variables
⍝← Variable Nv If NameList refers to a single column in the Database
⍝← NewDatabase W If NameList refers to multiple columns in the Database
⍝⍕ NewDatabase←[NameList] selectFrom Database
⍝⍎ selectFrom D where D.State eq 'PA'
⍝⍎ 'Height,Party' selectFrom D where D.State in 'PA,NJ'
     ⎕IO←0 ⋄ ⎕ML←3
     0=⎕NC'⍺':⍵             ⍝ If no left argument, exit
     nms←(' '⎕R',')⍺      ⍝ Replace blanks with commas
     nms←toNestedVector nms ⍝ Convert to nested vector
     xpr←⍕nms'←⍵.('nms')'   ⍝ Build expression
     d←⍎xpr                 ⍝ Execute it
     1=≢nms:d               ⍝ If one item, create vector
     ns←⎕NS nms             ⍝ Else create namespace
     ns
 }
