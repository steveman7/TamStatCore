 outliers←{
⍝∇   Find outliers in a sample
⍝⍺   Method Bs 0=Quartile method, 1=Chauvernet's criterion
⍝⍵   Sample Xv Sample Data
⍝←   List   Xv List of outliers
⍝⍕   List←[Method|_0] outliers Sample
⍝⍎   50 ← outliers 9 10 10 10 11 50
⍝⍎   50 ← 1 outliers 9 10 10 10 11 50
     ⎕IO←0 ⋄ ⎕ML←3
     ⍺←0
     ⍺=1:⍵~{m s←(mean,sdev)⍵            ⍝ Get mean, sdev
         d←normal criticalValue<÷4×≢⍵   ⍝ Find Dmax
         (d≥(⍵-m)÷s)/⍵}⍣≡⍵              ⍝ Remove outliers and recalculate
     ⍺=2:'*'
     b←{⍵+1.5×1 ¯1×-/⍵}1 3 quartile ⍵   ⍝ Create lower and upper fence values
     (⍵ outside b)/⍵                    ⍝ Select values outside the fence
 }
