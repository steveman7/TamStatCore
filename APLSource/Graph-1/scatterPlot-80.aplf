 scatterPlot←{
 ⍝⍟ Copyright (C) 2018, 2023 by Stephen M. Mansour
 ⍝∇ Generate Scatterplot
 ⍝∊ Graphics
 ⍝⍺ [Symbols] Cv Symbols
 ⍝⍵ YValues   Yv Response Values
 ⍝⍵ XValues   Xv Predictor Values
 ⍝← Graphic W Scatter Plot
 ⍝⍕ [Symbols] scatterPlot [YValues] Xvalues
 ⍝⍎ scatterPlot show Height ShoeSize
 ⍝⍎ Sex scatterPlot show Height ShoeSize
     ⎕IO←0 ⋄ ⎕ML←3 ⋄ ⍺←'*'                       ⍝ Set System Variables
     0J1∊⍺:(⍺~0J1)∇ ⍵
     ⍝G←25 75⍴' '                                 ⍝ Initialize Graph
     G←26 76⍴' '
     A←(0=≢⍺)⊃⍺'*'
     A W←A{
         2=⍴⍴⍵:⍺ ∇(↓⍉⍵)                          ⍝ If matrix, make vv
         ⍺∧⍥{(1=≡⍵)∧2|⎕DR ⍵}⍵:'*'(⍺ ⍵)           ⍝ Both args numeric vectors
         1=≡⍵:⍺((1+⍳⍴⍵)⍵)                          ⍝ Simple Numeric vector on right
         2<≢⍵:⍺{W←((↑⍵){(⍺⍴⍨⍴⍵)⍵}∊1↓⍵)
             1=≢⍺:⍺ W ⋄ (⍺/⍨⍴↑⍵)W}⍵             ⍝ More than two numeric vectors on right
         ⍺ ⍵}⍵

     B←↑=⍥{⎕NULL≠⍵}/W ⋄ GP←(B⍴⍨≢A)/A             ⍝ Remove Nulls
     YX←B∘/¨W
     Output←GP{⍝0::'Error!'
         M←⊃(¯5 ¯10+⍴G)Scale¨⍵~¨⎕NULL            ⍝ Scale
         I←↓⍉⊃M[;2]
         EU←⊃I≥⊂⍴G                                ⍝ Check for Index Error
         EL←⊃I<0
         I←0⌈I⌊⊂¯1+⍴G                             ⍝ Keep indices within bounds
        ⍝I←¯1+↓⍉⊃M[;2]
         A←⍺ ⋄ (EU[;0]/A)←'∧' ⋄ (EU[;1]/A)←'>'
         (EL[;0]/A)←'∨' ⋄ (EL[;1]/A)←'<'
        ⍝ G[I]←↑¨⍺                                ⍝ Insert * for each pair
         G[I]←↑¨A
        ⍝K←2=≢¨⍺
         K←2=≢¨A
        ⍝ G[(⊂0 1)+K/I]←↑∘⌽¨K/⍺
         G[(⊂0 1)+K/I]←↑∘⌽¨K/A
         T←M[;0]                                 ⍝ Tick Size
         FS←{⍵≥1:'I10'                           ⍝ Format string
             'F10.',⍕-⌊10⍟⍵}¨T
         LB←M[;1]                                ⍝ Lower bounds
         S←LB+T×⍳¨5 8                            ⍝ Scale
         B←25⍴1 4/1 0                            ⍝ Expand
         YI←(B⍀(0⊃FS)⎕FMT 0⊃S),'│┤'[B]           ⍝ Build Y-axis
         XI←(1 81⍴1 9/'┬─')⍪' ',,(1⊃FS)⎕FMT 1⊃S  ⍝ Build X-axis
        ⍝  XI[0;⍳11]←YI[0;⍳10],'┼'                 ⍝ Insert tick marks
         XI[0;⍳10]←¯10↑'└'                        ⍝ Insert tick marks
        ⍝ (⊖1↓YI,G,' ')⍪87↑⍤1⊢XI                  ⍝ Assemble plot
         (⊖G,⍨YI⍪' ')⍪87↑⍤1⊢' ',XI              ⍝ Assemble plot
     }YX
     Type←'Graph'
     Style←'ScatterPlot'
     NS←⎕NS'Output' 'Type' 'Style' 'YX'
     0J1∊⍺:NS.Output
     NS
 }
