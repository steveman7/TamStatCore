 frequency←{
    ⍝∇  Obtain frequencies from a variable
    ⍝⍟  Copyright 2018 by Stephen Mansour
    ⍝∊  Graphical Function
    ⍝⍺  Bins Is Bins=1: don't range data; Bins>1: number of bins; Bins<0: bin with
    ⍝⍵  Data Xv Simple Vector
    ⍝←  Table    Em Two-column matrix [Values, Frequencies] or Contingency Table
    ⍝⍕  Table←[Bins] frequency Vector
    ⍝⍎  1 frequency 2 1 2 3 4 2 3             ⍝ Frequency Table with numeric data
    ⍝⍎  5 frequency Height                    ⍝ Frequency Table with 5 bins
    ⍝⍎  ¯20 frequency Weight                  ⍝ Bin width = 20 lbs
    ⍝⍎  frequency 'Joe,Bob,Joe,Steve,Bob,Joe' ⍝ Frequency table with character data
    ⍝⍎  frequency Sex Party                   ⍝ Contingency Table
     ⎕ML←3 ⋄ ⎕IO←0                            ⍝ Set system variables
     (2=⍴⍴⍵)∧80≠⎕DR ⍵:⍵                       ⍝ If already frequency, done
     (9=⎕NC'⍵'):{b←0≠⍵.⎕NC'events'            ⍝ If namespace
         b:0 1,⍪⍵.(events,⍨count-events)      ⍝ If binary, create matrix
         x←⍵.(mean sdev)normal randVar ⍵.count⍝ Generate random data
         frequency x}⍵                        ⍝ Create frequency
     w←{(⎕DR ⍵)∊80 160:toNestedVector ⍵       ⍝ Convert right argument
         (2≠≢⍵)∨~(⎕DR 1⊃⍵)∊83 163 323:⍵       ⍝
         (1∨.>1⊃⍵):⍵                          ⍝
         (toNestedVector↑⍵),[0.5]1⊃⍵}⍵        ⍝
     def←{(⎕DR ⍵)∊11 80 160 320 326:0         ⍝ Default no range
         (83=⎕DR ⍵~⎕NULL)∧20>range ⍵:≢∪⍵      ⍝ Small integer, range=1
         bins←5+⍳10                           ⍝ Number of bins
         i←1⌈15+.<(count ⍵)÷bins              ⍝ Put at least 10 in each group
         bins[i-1]}                           ⍝ Interval width
     ⍺←def w                                  ⍝ Default number of bins
     rnd←{⍺≤0:⍵ ⋄ ⍺×⌊0.5+⍵÷⍺}¨                ⍝ Range the data?
     V2←(2=≢⍵)∧1<≡⍵                           ⍝ One or two variables?
     s←0J1≡↑⍺                                 ⍝ If no left argument?
     R←(w~⎕NULL)def{0=≢⍵:⍺⍺ ⍺ ⋄ ↑⍵}⍺~0 0J1    ⍝ Number of bins?
     a←w{⍵=≢∪⍺:0
       ⍝ (1<↑⍵)∧326≠⎕DR ⍺~⎕NULL:⍵{(⌊10⍟⍵)round ⍵÷⍺-1}range ⍺ ⍝ Remove nulls and
         (1<↑⍵)∧326≠⎕DR ⍺~⎕NULL:⍵{(10*¯1+⌊10⍟⍵)round ⍵÷⍺-1}range ⍺ ⍝ Remove nulls and
         |⍵}R                                 ⍝ Get bin width
     V2∧s:a _showCrossTab↑_crossTab/⍵         ⍝ Enhanced Contingency table
     V2:↑_crossTab/⍺ rnd ⍵                    ⍝ Simple contingency table
     s:_showFreq a _freq w                    ⍝ Enhanced frequency table
     a _freq w                                ⍝ Simple frequency table
 }
