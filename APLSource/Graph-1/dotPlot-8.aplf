 dotPlot←{
 ⍝⍟ Copyright (C) 2018 by Stephen M. Mansour
 ⍝∇ Generate Dot Plot
 ⍝∊ Graphics
 ⍝⍺ [Width←1] Ns Width
 ⍝⍵ Data Xv Numeric Vector
 ⍝← Graphic W Dot Plot
 ⍝⍕ Graphic←[Width] dotPlot Xv
 ⍝⍎ dotPlot Height
 ⍝⍎ 10 dotPlot Weight
     ⎕IO←0 ⋄ ⎕ML←3 ⋄ ⍺←1     ⍝ Set system variables
     a←↑(⍺,1)~0J1            ⍝ Show variable option
     i n←↓⍉a frequency ⍵     ⍝ Get frequenciews
     b←(⌽⍳⌈/n)∘.<n           ⍝ Create boolean matrix
     z←⍕(' *'[b])⍪i          ⍝ Build dot plot
     (¯1↓z)⍪'-'⍪¯1↑z         ⍝ Insert line for scale
 }
