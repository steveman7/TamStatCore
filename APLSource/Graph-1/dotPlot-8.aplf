 dotPlot←{
 ⍝⍟ Copyright (C) 2018 by Stephen M. Mansour
 ⍝∇ Generate Dot Plot
 ⍝∊ Graphics
 ⍝⍺ Width (Default 1)
 ⍝⍵ Xv: Numeric Vector
 ⍝← Am Character Matrrix
 ⍝⍕ [W] dotPlot Xv
 ⍝⍎ dotPlot D.Height
 ⍝⍎ 10 dotPlot D.Weight
     ⎕IO←0 ⋄ ⎕ML←3 ⋄ ⍺←1     ⍝ Set system variables
     a←↑(⍺,1)~0J1            ⍝ Show variable option
     i n←↓⍉a frequency ⍵     ⍝ Get frequenciews
     b←(⌽⍳⌈/n)∘.<n           ⍝ Create boolean matrix
     z←⍕(' *'[b])⍪i          ⍝ Build dot plot
     (¯1↓z)⍪'-'⍪¯1↑z         ⍝ Insert line for scale
 }
