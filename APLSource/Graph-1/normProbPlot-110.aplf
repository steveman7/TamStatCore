 normProbPlot←{
 ⍝⍟ Copyright (C) 2022 by Stephen M. Mansour
 ⍝∇ Generate Normal Probability Plot
 ⍝∊ Graphics
 ⍝⍺ [Symbols] Cv Symbols
 ⍝⍵ Data Xv Data Values
 ⍝← Graphic W ScatterPlot  Character Matrix or Namespace
 ⍝⍕ Graphic←[Symbols] normProbPlot Data
 ⍝⍎ normProbPlot show Height      ⍝ Simple Normal probability Plot
 ⍝⍎ Sex normProbPlot show Height  ⍝ Differentiate by Sex
     ⎕IO←0 ⋄ ⎕ML←3 ⋄ ⍺←'*'
     X←⍵[⍋⍵]  ⍝ Sort the data
     N←≢X     ⍝
     F←((1 to N)-0.375)÷N+0.25
     Z←normal critVal≥F
     ⍺≡'*':Z scatterPlot X
     ⍺≡,0J1:0J1 scatterPlot Z X
    ⍝ NS←⍺[⍋⍵]scatterPlot Z X
     A←(~0J1∊⍺)↓0J1,(⍺~0J1)[⍋⍵]
     A scatterPlot Z X
   ⍝  NS.Style←'NormProbPlot'
   ⍝  NS
 }
