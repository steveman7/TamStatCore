 anova←{
 ⍝⍟ Copyright (C) 2018, 2023 by Stephen M. Mansour
 ⍝∇ Perform ANalysis Of VAriance (ANOVA)
 ⍝∊ Operator
 ⍝⍺ [Fixed]       Is Number of fixed factors;  Fixed∊{0,1,2,3}
 ⍝⍺ [Random←0]    Is Number of random factors  Random∊{0,1,2}
 ⍝⍺ [LS]          Cm N×N matrix
 ⍝⍺ [Alias]       Cv Source(s) of variation conflated with error.
 ⍝⍺⍺ design f       oneWay, blocked, latinSquare, factor, factorial2k, nested
 ⍝⍵ Database      W  Contains exactly one Numeric variable and one or more character variables
 ⍝⍵ Response      X  Partitioned Vector, Simple Vector, Matrix or Array, or Summary Namespace
 ⍝⍵ Levels        Cv For one-way Anova
 ⍝⍵ Factors       Cn For multi-factor Anova
 ⍝← NS:   Namespace containing the following variables:    AnovaTable  RSq  RSqAdj  S  Type
 ⍝⍕ Model ← oneWay|blocked|nested|[LS]latinSquare|[Fixed [Random]]factor|[Alias]factorial2k anova DataBase|Response Levels|Factors]]
 ⍝⍎ report oneWay anova Environment Party         ⍝ One-way ANOVA with Grouping Variable
 ⍝⍎ report oneWay anova Group1 Group2 Group3      ⍝ One-way Anova with Partioned Vector
 ⍝⍎ report N factor anova Array                   ⍝ Factorial Design
 ⍝⍎ report factorial2k anova Matrix               ⍝ 2*k factorial design
 ⍝⍎ report 'ABC' factorial2k anova Matrix         ⍝ 1/2 Fractional Factorial Design
 ⍝⍎ report 'ABC' 'DEF' factorial2k anova Matrix   ⍝ 1/4 Fractional Factorial Design
 ⍝⍎ report oneWay anova 'Height Party' selectFrom DB ⍝ Uses all variables
 ⍝⍎ report oneWay anova DB 'Environment Party'     ⍝ One-way ANOVA using factor name
 ⍝⍎ report oneWay anova stats¨(5 10 3)(5 12 2)(6 11 4)   ⍝ One-Way anova with summary data
 ⍝⍎ report 2 factor anova 2 3 ⍴1.3 2.7 3.5 1.4 3.2 3.9   ⍝ Executable Example
 ⍝⍎ anova MPG AUTO GAS                     ⍝ Two-way ANOVA
     ⎕IO←0 ⋄ ⎕ML←3 ⋄ ⍺←⍬                   ⍝ Set system variables
     NL X←{isNamespace 1⊃2↑(,⍵),0:'a'⍵
         isNamespace↑⍵:{1=≢⍵:⍵.(⊢{⍺ ⍵}⍎¨)variables ⍵
             (' 'toNestedVector 1⊃⍵)(⍵[0]⍎1⊃⍵)}⍵
         (2≤⍴⍴⍵):⍬(⊃⍵)                  ⍝ If not a vector and nested, disclose
         (,2)≡⍴⍵:((1↓⎕D)↑⍨⍴⍵)⍵          ⍝ Balanced anova
         ((1↓⎕D)↑⍨⍴⍵)⍵}⍵                ⍝ One factor unbalanced
     TBL←⍺ ⍺⍺ X ⋄ EFF←TBL[;0]
     EFF←EFF{NS←↑⍵ ⋄
         isNamespace 1⊃(,⍵),0:⍺
         2=≢⍺:⍺
         ⍝(2<≢⍵):⍺
         (9=⎕NC'NS')⍲'A'≡⍥,↑⍺:⍺
         NMS←{1=≢⍵:variables ⍵ ⋄ {1↓(⍵≠' ')⊂⍵}1⊃⍵}⍵
         S←(2=≡NMS)/'*'
         Z←{↑{⍺,'*',⍵}/NMS[⍵]}¨⎕A∘⍳¨⍺~E←⊂'Error'

         Z,(E∊⍺)/E}⍵
     NS←EFF buildAnovaTable 0 1↓TBL
     NS.DF2←{0::⍬
         ¯1↓⍵.AnovaTable[TBL[;3];2]}NS         ⍝ Denominator degrees of freedom
     NS.Type←'ANOVA'                         ⍝ Namespace type is ANOVA
    ⍝ NS.Omega2←calcw2 NS.AnovaTable
     NS←NS{0=⍺.⎕NC ⍵:⍺
         ⍺.Omega2←calcw2 ⍺⍎⍵ ⋄ ⍺}'AnovaTable'
     2≠≢EFF:NS                                ⍝ If 2 or more factors, exit
     A X←NL{2=⍴⍴⍵:(⍕¨1+⍳≢⍵)(↓⍵)               ⍝ Separate Names, Data
         isNamespace↑⍵:{b←0=⍵[0].⎕NC'name'    ⍝ If names not included
             b:(,¨(≢⍵)↑⎕A)⍵                   ⍝  Use A,B,C
             ⍵.name ⍵}⍵                       ⍝ Else use specified names
         2<≢⍵:⍺ ⍵
         ↓⍉↑{⍺ ⍵}⌸/⌽⍵}X
     H←'Group' 'N' 'Mean' 'StdDev'            ⍝ Build Means Table
     NS.Means←H⍪A,⊃(count,mean,sdev)¨X
    ⍝ NS.Omega2←{(⍵[1;1]-⍵[1;2]×⍵[2;3])÷⍵[3;1]+⍵[2;3]}NS.AnovaTable
     NS.LeftArg←⍺
     isNamespace 1⊃X:NS                       ⍝ Exit if summary data
     NS.YHAT←(≢¨X)/1↓NS.Means[;2]             ⍝ Fitted Values
     NS.E←(∊X)-NS.YHAT                        ⍝ Residuals
     NS
 }
