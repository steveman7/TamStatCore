 anova←{
 ⍝⍟ Copyright (C) 2018, 2023 by Stephen M. Mansour
 ⍝∇ Perform ANalysis Of VAriance (ANOVA)
 ⍝∊ ANOVA
 ⍝⍺ Number of levels (2 or 3)
 ⍝⍺ Number of factors
 ⍝⍺⍺ * for 2k or 3k design
 ⍝⍺⍺ Number of factors
 ⍝⍺⍺ Name list of factors:
 ⍝⍺⍺ *  if 2k or 3k design
 ⍝⍵ X:  Response (simple or partitioned array
 ⍝⍵ NS: Database                            `
 ⍝⍵ [A1: Factor 1]
 ⍝⍵ [A2: Factor 2]
 ⍝← NS:   Namespace containing the following variables
 ⍝        AnovaTable  RSq  RSqAdj  S  Type
 ⍝⍕ NS ← oneWay|factor|factorial2k anova X [Av1 [Av2]]
 ⍝⍎ report oneWay anova D.Environment D.Party     ⍝ One-way ANOVA
 ⍝⍎ report oneWay anova Group1 Group2 Group3
 ⍝⍎ report N factor anova Array
 ⍝⍎ report factorial2k anova Matrix
 ⍝⍎ report 'ABC' factorial2k anova Matrix
 ⍝⍎ report 'ABC' 'DEF' factorial2k anova Matrix
 ⍝⍎ report anova D 'Environment Party'     ⍝ One-way ANOVA using factor name
 ⍝⍎ report anova oneWay stats¨(N1 S1 X1)...(Nn Sn Xn)
 ⍝⍎ report 2 factor anova 2 3 ⍴1.3 2.7 3.5 1.4 3.2 3.9
 ⍝⍎ anova MPG AUTO GAS                     ⍝ Two-way ANOVA
     ⎕IO←0 ⋄ ⎕ML←3 ⋄ ⍺←⍬                   ⍝ Set system variables
     NL X←{isNamespace 1⊃,⍵:'a'⍵
         isNamespace 0⊃,⍵:(' 'toNestedVector 1⊃⍵)(⍵[0]⍎1⊃⍵)
       ⍝  (2≤⍴⍴⍵):⍬ ⍵ ⋄ ((1↓⎕D)↑⍨⍴⍵)⍵}⍵
         (2≤⍴⍴⍵):⍬(⊃⍵)                  ⍝ If not a vector and nested, disclose
         (,2)≡⍴⍵:((1↓⎕D)↑⍨⍴⍵)⍵
        ⍝ 1=≢∪≢¨⍵:⍬(⊃⍵)                ⍝ Balanced anova
         ((1↓⎕D)↑⍨⍴⍵)⍵}⍵                ⍝ One factor unbalanced
     TBL←⍺ ⍺⍺ X ⋄ EFF←TBL[;0]
     EFF←EFF{NS←↑⍵ ⋄
         isNamespace 1⊃,⍵:⍺
         ⍝(2<≢⍵):⍺
         (9=⎕NC'NS')⍲'A'≡⍥,↑⍺:⍺
         NMS←{1↓(⍵≠' ')⊂⍵}1⊃⍵
        ⍝Z←{∊NMS[⍵]}¨⎕A∘⍳¨⍺~E←⊂'Error'
         S←(2=≡NMS)/'*'
         Z←{↑{⍺,'*',⍵}/NMS[⍵]}¨⎕A∘⍳¨⍺~E←⊂'Error'

         Z,(E∊⍺)/E}⍵
     NS←EFF buildAnovaTable 0 1↓TBL
    ⍝ NS.DF2←¯1↓NS.AnovaTable[TBL[;3];2]     ⍝ Denominator degrees of freedom
     NS.DF2←{0::⍬
         ¯1↓⍵.AnovaTable[TBL[;3];2]}NS        ⍝ Denominator degrees of freedom
     NS.Type←'ANOVA'                         ⍝ Namespace type is ANOVA
     2≠≢EFF:NS                               ⍝ If 2 or more factors, exit
     A X←NL{2=⍴⍴⍵:(⍕¨1+⍳≢⍵)(↓⍵)              ⍝ Separate Names, Data
         isNamespace↑⍵:{b←0=⍵[0].⎕NC'name'   ⍝ If names not included
             b:(,¨(≢⍵)↑⎕A)⍵                  ⍝  Use A,B,C
             ⍵.name ⍵}⍵                      ⍝ Else use specified names
         2<≢⍵:⍺ ⍵
         ↓⍉↑{⍺ ⍵}⌸/⌽⍵}X
     H←'Group' 'N' 'Mean' 'StdDev'           ⍝ Build Means Table
     NS.Means←H⍪A,⊃(count,mean,sdev)¨X
     NS.LeftArg←⍺
     isNamespace 1⊃X:NS                      ⍝ Exit if summary data
     NS.YHAT←(≢¨X)/1↓NS.Means[;2]            ⍝ Fitted Values
     NS.E←(∊X)-NS.YHAT                       ⍝ Residuals
     NS
 }
