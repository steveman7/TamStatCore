 pacf←{
⍝ F←pacf R;RR;N;⎕IO
⍝@ (c) Copyright 2025 by Stephen Mansour
⍝∇ Partial Autorocorrelation Function
⍝R Xv VECTOR OF AUTOCORRELATIONS STARTING WITH LAG 1
⍝F Xv VECTOR OF PARTIAL AUTOCORRELATIONS
 ⍝⎕IO←0 ⋄ ⎕ML←3
⍝ RR←(1,R)[|N∘.-N←⍳1+⍴R]
⍝ N←1 ⋄ F←⍳0
⍝L1:F←F,↑⌽(N↑R)⌹N N↑RR
⍝ →((⍴R)≥N←N+1)/L1
     ⎕IO←0 ⋄ ⎕ML←1 ⋄ ⍺←0
     R←acf ⍵
     RR←(1,R)[|N∘.-N←⍳1+⍴R]
     F←⌽(≢R){⍺=0:⍵
         Z←⍵,⊃⌽(⍺↑R)⌹⍺ ⍺↑RR
         (⍺-1)∇ Z}⍬
     ~⍺=0J1:F                                 ⍝ If show, display graph
     I←round F×25                          ⍝ Scale and round output
     B←(¯27 to 26)include⍤1⊢(I<0)⌽0,⍪I     ⍝ Generate pattern
     D←'I3,M<->F9.3'⎕FMT(1+⍳⍴F),⍪F          ⍝
     G←D,' X'[B]                           ⍝ Display output
     H←'Lag    Corr',,'M<->F5.1'⎕FMT ¯1 to 1,0.2
     H←↑H('---    -----  ',51⍴'+----')     ⍝ Build header
     H⍪G
 }
