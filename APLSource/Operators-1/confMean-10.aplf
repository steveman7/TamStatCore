 confMean←{⎕IO←0 ⋄ ⎕ML←3                   ⍝ Conf interval for mean
 ⍝⍺ Confidence Level
 ⍝⍵ Data Representation

     C N←⍺
     f1←{C N←⍺ ⋄ ⎕DIV←1                    ⍝ Mean estimate
         rAv:C{⍝str←'t.test(⍵,conf.level=⍵)'⍝ build R expression
             x←{2=⍴⍴⍵:⊃(//↓⍉⌽⍵) ⋄ ⍵}⍵      ⍝ Input to R expression
             ~'X'##.∆r.set x:'Error'
            ⍝u←(↑⍺){+##.∆r.x str ⍵ ⍺}¨⊆x   ⍝ Use R if requested
             u←'t.test(X,conf.level=',(⍕⍺),')$conf.int[c(1,2)]'
             ##.∆r.eval u}⍵                 ⍝ Obtain value from frame
         m←mean ⍵                          ⍝ Sample Average
         v←var ⍵                           ⍝ Sample Variance
         n←count ⍵                         ⍝ Sample Sizes
         se←sqrt(1-(×N)×n÷N⌈1)×v÷n         ⍝ Standard Error w/optional finite correction factor
         df←n-1                            ⍝ Degrees of freedom
         t←df tDist criticalValue<(1-C)÷2  ⍝ Get 2-tail t critical value
         me←t×se                           ⍝ Margin of Error
         m(-,+)me}                         ⍝ LCL, UCL
     f2←{rAv:⍺{                            ⍝ Difference of means
            ⍝str←'t.test(⍵,⍵,conf.level=⍵)'⍝ Build R expression
             x←{2=⍴⍴⍵:⊃(//↓⍉⌽⍵) ⋄ ⍵}¨⍵     ⍝ Convert freq to data
             ↑⍲/'XY'##.∆r.set¨x:'Error'
             u←'t.test(X,Y,conf.level=',(⍕⍺),')$conf.int[c(1,2)]'
             ##.∆r.eval u}⍵  ⍝ Use R if requested

         m←-/mean¨⍵                        ⍝ Get mean(s)
         n←count¨⍵                         ⍝ Get sample size(s)
         v←(variance¨⍵)÷n                  ⍝ Get sampling variance(s)
         se←(+/v)*÷2                       ⍝ Standard Error(s)
         f3←{((+/⍵)*2)÷(⍵*2)+.÷⍺-1}        ⍝ Calculate degrees
         df←n f3 v                         ⍝   of freedom
         t←df tDist criticalValue<(1-⍺)÷2  ⍝ Get 2-tail t critical value
         me←t×se                           ⍝ Margin of Error
         m(-,+)me}                         ⍝ LCL, UCL
     twoSample ⍵:C f2 ⍵ ⋄ ⍺ f1 ⍵}
