 criticalValue←{
 ⍝⍟ Copyright (c) 2018 by Stephen M. Mansour
 ⍝∇ Calculate a critical value of a distribution
 ⍝∊ Operators
 ⍝⍺ [Parameters]:   Distribution parameters (default 0 1)
 ⍝⍺⍺ fD:  Distribution function
 ⍝⍵⍵ fR:  Relational function
 ⍝⍵  P:    Probability or area 0<P<1
 ⍝← X:     Value
 ⍝⍕ X ←  [Parms|0 1] fD criticalValue fR P
 ⍝⍎ ¯1.645 ← normal criticalValue > 0.05         ⍝ Lower tail standard normal
 ⍝⍎ 15.086 ← 5 chiSquare criticalValue > 0.01    ⍝ Upper tail chi-Square
 ⍝⍎ 2.1788 ← 12 tDist criticalValue outside 0.95 ⍝ 2-tail Student t
     ⎕ML←3 ⋄ ⎕IO←0 ⋄ ⍺←0 1                       ⍝ Default parameters (0,1)
     1<|≡⍵:⍺∘∇ pervasive ⍵                       ⍝ Allow nested input
     ~'P'check ⍵:⎕SIGNAL 11                      ⍝ Must be probability
     a←⍺,¯1                                      ⍝ Append ¯1 for inverse
     b←0 1 ⍵⍵ 1 0                                ⍝ Test relation
     b←b∨1≡2 ⍵⍵ 1 3                              ⍝ If between, same as ≠
     p←b{≠/⍺:|⍵-↑⍺ ⋄ w←⍪⍣(×↑⍴⍴⍵)⊢⍵
         q⌽(⊢,1∘-)2÷⍨w+q←~↑⍺}⍵                   ⍝ Find probability
     2=⎕NC'⍺⍺':⍺⍺{                               ⍝ If numeric left operand
         v q←↓⍉frequency ⍺⍺                      ⍝
⍝         v[+/⍵∘.≥+\q÷+/q]}p                     ⍝
         b←⍵∘.⍵⍵⍨+\q÷+/q                         ⍝
         v[+/b=0 ⍵⍵ 1]}⍵⍵ p                      ⍝ Flip if < or ≤
     d←('Discrete'≡⍺⍺'Type')∧0 1=.⍵⍵ 1 1         ⍝ If discrete and > or ≤
     p←p+d×2×⎕CT                                 ⍝ Adjust p
     2>≡p:9○a ⍺⍺ p                               ⍝ Simple right argument
     9○a∘⍺⍺¨p                                    ⍝ Enclosed or nested arguemnt
 }
