 bootstrap←{
 ⍝⍟ Copyright (C) 2018 by Stephen M. Mansour
 ⍝∇ Bootstrap resampling
 ⍝∊ Operators
 ⍝⍺ N: Number of times to sample with replacement
 ⍝⍺⍺ fS: Summary Function
 ⍝⍵ Xv:  Sample data
 ⍝⍵ Xm:  2-column matrix: [midpoints|frequencies]
 ⍝⍵ NS: Namespace containing count, mean, sdev
 ⍝← Vector of Sample Statistics
 ⍝⍕ Sample ← N fS bootstrap Xv|Xm|NS
 ⍝⍎ 100 mean bootstrap normal randomVariable 10
 ⍝⍎ 100 median bootstrap 1 exponential randomVariable 25
 ⍝⍎ 67.581 69.419 ← mean confInt stats 49 68.5 3.2
     ⍺←1000         ⍝ Default =  1000 iterations
     n←≢⍵           ⍝ Original Sample Size
     ⍺⍺⍤1⊢⍵[?⍺ n⍴n] ⍝ Resample with replacement; apply summary fn
 }
