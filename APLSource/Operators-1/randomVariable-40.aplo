 randomVariable←{
 ⍝⍟ Copyright (c) 2018, 2024 by Stephen M. Mansour
 ⍝∇ Simulate sampling from a specified distribution
 ⍝∊ Operator
 ⍝⍺ [Parms]        Xv   Distribution parameters (default 0 1)
 ⍝⍺⍺ Distribution  fD   Probability Distribution
 ⍝⍺⍺ FreqDist      Xm   Two-Column Matrix:  Values, Probabilities
 ⍝⍵  SampleSize    N    Number of samples to simulate, use 0 for scalar.
 ⍝←  SampleData    Xv   Random values from fD
 ⍝⍕  SampleData ←  [Parms|_0_1] fD|FreqDist randomVariable SampleSize|0
 ⍝⍎  2 1 5 4 2←7 .3 binomial randomVariable 5   ⍝ Binomial Random Variables
 ⍝⍎  ¯0.98264 1.07195 0.34821←normal randomVariable 3          ⍝ Standard Normal Random Variables
     ⎕IO←0 ⋄ ⎕ML←3
     ⍺←0 1                     ⍝ Default parameters
     2=⎕NC'⍺⍺':⍺⍺{             ⍝ If left operand is a variable
        ⍝ x p←↓⍉frequency ⍺    ⍝ Separate values, probabilities
         x p←{2≠⍴⍴⍵:⍵          ⍝ If two vectors, done
             ↓⍉frequency ⍵}⍺   ⍝ Else separte values, probabilities
         u←?⍵⍴0                ⍝ Generate random Variable
         x[+/u∘.≥+\p÷+/p]}⍵    ⍝ Select from distribution
     1<≡⍺:(↑,¨/⍺)∇¨⍵           ⍝ If nested parameters, make pervasive
     w←{(⊂⍵)∊0 '':⍬ ⋄ ⍵}⍵      ⍝ If 0, treat as scalar
     w ⍺⍺⍨⍺'r'                 ⍝ If simple parameter list just calculate
 }
