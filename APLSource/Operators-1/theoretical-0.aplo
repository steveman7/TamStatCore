 theoretical←{
 ⍝⍟ Copyright (C) 2018 by Stephen M. Mansour
 ⍝∇ Calculate parameter  for a theoretical distribution
 ⍝∊ Operators
 ⍝⍺ [Parms←0&nbsp.1] Xv Parameter List for Distribution
 ⍝⍺⍺ fD fD
 ⍝⍵⍵ fS fS
 ⍝⍵  Dummy X   Dummy value unless fS requires a left argument
 ⍝←  Value X   Result of summary function applied to a population with distribution fD
 ⍝⍕  Value←[Parms|_0_1]fD theoretical fS Dummy
 ⍝⍎  2.1 ← 7 .3 binomial thoretical mean ''
 ⍝⍎  0.9295 ← 13 5 52 hyperGeometric theoretical sdev ''
 ⍝⍎  0.6745 ← normal theoretical quartile 3
     ⎕IO←0 ⋄ ⎕ML←3 ⋄ ⍺←0 1
     (2=⎕NC'⍺⍺')∧⍵≠0:⍵ ⍵⍵ ⍺⍺               ⍝ If variable left arg, apply fn
     2=⎕NC'⍺⍺':↑⍵⍵ ⍺⍺
     f←⍺⍺{d←3 ⍵⍵ 2 2 3 5 7
         d=6:⍺ ⍺⍺ criticalValue≥⍵÷4        ⍝ Quartile
         d=2:⍺ ⍺⍺ criticalValue≥⍵÷100      ⍝ Percentile
         d=60:100×⍺ ⍺⍺ probability<⍵       ⍝ PercentileRank
     }⍵⍵
     ⍵≠0:⍺ f ⍵
     d←⍵⍵ 2 2 3 5 7                        ⍝ Apply function to known data
     d=3.8:⍺ ⍺⍺'Mean'                      ⍝ Mean
     d=4.7:⍺ ⍺⍺'Variance'                  ⍝ Variance
    ⍝ 4.7=d*2:(⍺ ⍺⍺'Variance')*÷2           ⍝ Standard Deviation
     4.7=d*2:⍺⍺{z←⍵ ⍺⍺'Variance'
         sqrt z}⍺
       ⍝d=3:⍺ ⍺⍺ criticalValue≤0.5            ⍝ Median
     d=3:⍺ ⍺⍺'Median'
     d=2:⍺ ⍺⍺'Mode'                        ⍝ Mode
     d=4:-/⍺ ⍺⍺ criticalValue≤0.25 0.75    ⍝ iqr
     d<0:⍺ ⍺⍺'Kurtosis'                    ⍝ Kurtosis
     d<1:⍺ ⍺⍺'Skewness'                    ⍝ Skewness

 }
