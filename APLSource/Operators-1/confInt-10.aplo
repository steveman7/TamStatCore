 confInt←{
 ⍝⍟ Copyright (C) 2018, 2023 by Stephen M. Mansour
 ⍝∇ Calculate a confidence interval for a parameter or response variable
 ⍝∊ Operators
 ⍝⍺ [Level]: Confidence level - default .95
 ⍝⍺ [N]: Population Size (required for sum)
 ⍝⍺⍺ fS: Summary Function
 ⍝⍺⍺ fL: Linear Function  f(X) =  b0 + b1*x1 + ... + bn*xn
 ⍝⍵ X:        Raw data - numeric vector
 ⍝⍵ Xm:  2-column matrix: [midpoints|frequencies]
 ⍝⍵ NS: Namespace containing count, mean, sdev
 ⍝⍵ [A]: Grouping nested character vector
 ⍝← Interval:   2-item vector
 ⍝⍕ Interval ← [.95|P][N] fS|fL confInt X[A]|Cm|NS
 ⍝⍎ mean confInt Height
 ⍝⍎ mean confInt Height Sex
 ⍝⍎ 67.581 69.419 ← mean confInt stats 49 68.5 3.2
 ⍝⍎ MODEL.f confInt 10
     ⍺←0.95 ⋄ ⎕ML←3 ⋄ ⎕IO←0                    ⍝ Default conf level is 95%
     'Linear'≡⍺⍺'Type':⍺ 1 ⍺⍺ ⍵                ⍝ If regression
  ⍝   L←'Linear'≡⍺⍺'Type'                       ⍝ If regression
  ⍝   L:⍺ 1 ⍺⍺ ⍵                           ⍝ Single value
⍝    L∧0≠≡⍵:⊃⍺ 1∘⍺⍺¨⍵                          ⍝ Multiple value
     x←⊂⍣(326≠⎕DR ⍵)⊣⍵                         ⍝ Enclose if simple numeric array
     2<≢x:'No more than two right arguments permitted'
     x←{(9∊⎕NC¨'⍵')∨0 1≢11=⎕DR¨⍵:⍵             ⍝ If 2nd arg boolean
         ↑{(⍵/⍺)((~⍵)/⍺)}/⍵}x                  ⍝ Partition into two groups
     G←(2=≢x)∧(⎕DR⊃↑⌽x)∊80 160 320             ⍝ If 2nd arg character
     NL x←G{⍺:↓⍉↑{⍺,⊂⍵}⌸/⌽⍵ ⋄ ' '⍵}x           ⍝ Group the data
     NL x←G{⍺:(1<≢¨x)∘⌿¨⍵ ⋄ ⍵}NL x             ⍝ Eliminate 1-item groups
     i←↑⍺⍺ 3 9 12                               ⍝ Test Operand on known data
     p←stats∘{i=¯1:round(count,mean×count)⍵    ⍝ If proportion
         (count,mean,sdev)⍵}¨x                 ⍝ Convert to summary form
     0J1∊⍺:((⍺~0J1)G NL)⍺⍺ formatConfInt(i=2)⊃p x
     C N←{1∧.>∊⍵:⍵ 0 ⋄ 2=≢⍵:⍵[⍋⍵] ⋄ 0.95,⍵}⍺   ⍝ Conf Level, Population Size
     G∧1<≡x:⊃C∘.∇ x                            ⍝ Multiple conf levels and
     1<≢C:⊃C ∇¨⊂x                              ⍝ Multiple conf levels?
     confMed←{C N←⍺ ⋄ n←≢d←⍵[⍋⍵]               ⍝ Sort the data
         cp←n 0.5 binomial prob≤⍳n+1           ⍝ Cumulative probabilities
         p←{⍵,1-⍵}(1-C)÷2                      ⍝ Upper lower probs
         d[0 1+{⍵/⍳⍴⍵}2≠/≠⌿p∘.<cp]}            ⍝ Get conf int
     f←C N∘{i=0:⍺ confCorr x                   ⍝ Correlation
         i=24:⍺ confTotal ⍵                    ⍝ Total
         i=8:⍺ confMean ⍵ x                    ⍝ Mean
         i=¯1:⍺ confProp ⍵                     ⍝ Proportion
         i=21:⍺ confVar ⍵                      ⍝ Variance
         i=21*÷2:0.5*⍨⍺ confVar ⍵              ⍝ Standard Deviation
         i=9:⊃⍺∘confMed¨x                      ⍝ Median
         'illegal operand'}                    ⍝
     f p                                       ⍝
 }
