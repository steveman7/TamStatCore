 confInt←{
 ⍝⍟ Copyright (C) 2018, 2023 by Stephen M. Mansour
 ⍝∇ Calculate a confidence interval for a parameter or response variable
 ⍝∊ Operator
 ⍝⍺ [Level←0.95] Ps Confidence level - default .95
 ⍝⍺ [N←0]  N   Population Size (required for sum); infinite if not specified
 ⍝⍺⍺ fS  fS  Summary Function
 ⍝⍺⍺ fM  fm  Multivariate function; typically linear:  f(X) =  b0 + b1*x1 + ... + bn*xn
 ⍝⍵  Sample1   Xv Sample Data
 ⍝⍵  [Sample2] Xv Second Sample Data
 ⍝⍵_  [Groups]  Cv Grouping Data
 ⍝⍵  FreqDist Xm  2-column matrix: [midpoints|frequencies]
 ⍝⍵  SummaryDat W  Summary Data including count, mean, sdev
 ⍝← Interval:  Xv Lower and Upper Bounds
 ⍝⍕ Interval ← [.95|Level][N] fS|fM confInt Sample1 [Sample2|Groups]|FreqDist|SummaryData
 ⍝⍎ mean confInt Height          ⍝ 95% Confidence Interval for Mean Height
 ⍝⍎ 0.77344 80715←0.9 proportion confInt Sex eq 'M' ⍝ 90% Confidence interval for Male Proportion
 ⍝⍎ 11.81 24.496←0.99 var confInt Height       ⍝ 99% Confidence interval Height variance
 ⍝⍎ mean confInt Height splitBy Sex            ⍝ Difference between Male and Female
 ⍝⍎ 67.581 69.419 ← mean confInt stats 49 68.5 3.2 ⍝ Summary Data
 ⍝⍎ MODEL←Height regress ShoeSize ⍝ Regression Model
 ⍝⍎ 68.378 69.274←MODEL.f confInt 10           ⍝ Estimate Height Confidence Interval for size 10 Shoe
     ⍺←0.95 ⋄ ⎕ML←3 ⋄ ⎕IO←0                    ⍝ Default conf level is 95%
     'Linear'≡⍺⍺'Type':⍺ 1 ⍺⍺ ⍵                ⍝ If regression, conf int for y
     'Association'≡⍺⍺'Type':⍺ ⍺⍺{⍺≠⍥≢↑⊆⍵:⍺ ⍺⍺ ⍵  ⍝ If bivariate function
         0.95 ⍺⍺ ⍺ ⍵}⍵                         ⍝    check left argument
     x←⊆⍣(V←2>↑⍴⍴⍵)⊢⍵                          ⍝ Enclose if simple numeric vector
     V∧2<≢x:'No more than two right arguments permitted'
     x←{(9∊⎕NC¨'⍵')∨0 1≢11=⎕DR¨⍵:⍵             ⍝ If 2nd arg boolean
         ↑{(⍵/⍺)((~⍵)/⍺)}/⍵}x                  ⍝ Partition into two groups
     C←80 160 320                              ⍝ Character codes
     x←{↑⊂groupBy/⍵}⍣(C∊⍨⎕DR⊃↑⌽⍵)⊢x            ⍝ Group the data?
     A←⍺~0J1
     CL←{0=≢⍵:0.95 ⋄ ↑⍣(1=≢⍵)⊢⍵}(645=⎕DR¨A)/A  ⍝ Find CL where 0<CL<1
     M←{0=≢⍵:0 ⋄ W←↑⍣(1=≢⍵)⊢⍵                  ⍝ Disclose singletons
         toNestedVector W}A~CL,⊂CL              ⍝ Find population size or method(s)
     NL x←{~C∊⍨⎕DR↑⍵:' '⍵                      ⍝ If grouped data,
         w←(1<≢¨0⌷[1]⌽⍵)⌿⍵                     ⍝ Eliminate 1-item groups
         2=↑⍴⍉w:↓⍉w                            ⍝ One set of groups
         NL x←↓⍉⊃¨w⊂⍨2-C∊⍨⎕DR¨w[0;]
         (toDelimitedList¨NL)(,¨x)}x           ⍝ Multiple sets of groups
     0J1∊⍺:(CL M NL)⍺⍺ formatConfInt x         ⍝ Format using "show" operator
     M←↑¨⎕C M
    ⍝(twoSample x)∧1=≢NL:⊃(CL,¨M)⍺⍺¨⊂x         ⍝ Two-sample case
     (twoSample x)∧1=≢NL:⊃(CL∘.,M)⍺⍺¨⊂x       ⍝ Two-sample case
     K←≢¨CL M x                                ⍝
     K∧.=1:(CL,M)⍺⍺↑x                          ⍝ Simple case - 2-vector result
    ⍝ ⊃(CL,¨M)∘.⍺⍺ x
     2=⍴⍴x:⊃(CL∘.,M)∘.⍺⍺⊂x                     ⍝ Frequency distribution
     ⊃(CL∘.,M)∘.⍺⍺ x                           ⍝ Raw data or namespace
 }
