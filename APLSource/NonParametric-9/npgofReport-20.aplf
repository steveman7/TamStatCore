 npgofReport←{
  ⍝ Non-Parametric GoodnessOfFit Report

     ⎕IO←0 ⋄ ⎕ML←3
     NN←+/N←⍵.SampleSize
     J←1+{Q⍳⍤1 0⊢⌈/Q←⍉1↓¯2↑[1]⍵}⍵.Table         ⍝ Find maximum values
     T←⍵.Table,' *'[J∊⍨⍳1+NN]                  ⍝ Flag T+ and T-
     I←NN{⍺<15:⍳⍺+1 ⋄ sort 0,(1+⍳5),⍵,(⍺-⍳5)}J
     T←T,⍨'i',1+⍳NN                              ⍝ Append sample index
     T←{⎕PP←5 ⋄ '-'@('¯'∘=)⍕⍵}T[I;]             ⍝ Format 5 decimal places, replace hi-minus
     T←NN{⍺<15:⍵ ⋄ (6↑⍵)⍪'.'⍪⍵[6;]⍪'.'⍪⍵[7;]⍪'.'⍪¯5↑⍵}T
     R←⌊/|-\⌽7,2⊥(⊢⍵.rel⌽)1+⍳3                   ⍝ (>,1),(=,2),(<,3)
   ⍝  PA←⍕{6::' ' ⋄ ' ',[-0.5](≢⍵.Parameters)↑'Mean:' 'Standard Deviation:',¨2↑⍵.Parameters}⍵
     PA←{6::' ' ⋄ (≢⍵.Parameters)↑'Mean:' 'Standard Deviation:',¨2↑⍵.Parameters}⍵
    ⍝ PA←' ',[¯0.5]⍕PA('Sample Size:  N ='N)
     PA←' ',[¯0.5]⍕{1≡≢⍵:PA('Sample Size: N = '⍵)
         'Sample Size: N = ',⍵[0],'M =',⍵[1]}N
     tb←⊃'Test Statistic: '('T=',⍕⍵.TestStatistic)
     tb←tb(⊃'P-Value: '('p'fmtP ⍵.P))
     A←⍺÷2*R=2            ⍝ If two-sided, divde significance by 2
     x←A{f←{⍵<5.5:1 ⋄ ⍵-0.01-0.83÷⍵}∘sqrt
         ⍵≡'normal':(f N)÷⍨(31⌊N)(A14 tableLookup)1-⍺
         ⍵≡'exponential':(1⌈0.1×sqrt N)÷⍨(100⌊N)(A15 tableLookup)1-⍺
         ⍵≡'kolmogorov':N kolmogorov critVal<⍺
         =/N:(N[0]*0.5*N[0]≥41)÷⍨(41⌊N[0])(A19 tableLookup)1-⍺
         sqrt(-⍟⍺)×0.5×(+÷×)/N
     }⍵._Distribution
     cv←' '~⍨,'<T(α)=>,F10.3'⎕FMT x
     tb←tb,(⊃'Critical Value:'cv)(⊃'Significance Level:'('α=',⍕⍺))
     h←'H₀:' 'H₁:',¨(R-1){R0←⍺⊃'≤=≥' ⋄ R1←⍺⊃'>≠<'
         ⍵≡'kolmogorov':{'F(x)',⍵,'F*(x)'}¨R0 R1
         ⍵≡'smirnov':{'F(x)',⍵,'G(x)'}¨R0 R1
         ⍵('not ',⍵)}⍵._Distribution
     tb←tablebox 2 2⍴tb
     C←(⍵.P<⍺)⊃'Fail to reject H₀' 'Reject H₀'
     TITLE←{(⊂⍵)∊'exponential' 'normal':'Lillefors' ⋄ ⍵}⍵._Distribution
     ⍕,[⍬](TITLE,' Test')T PA' 'h tb('Conclusion:'C)


 }
