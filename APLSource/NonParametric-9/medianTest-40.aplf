 medianTest←{
⍝⍺ (Optional) grouping vector
⍝⍵ matrix simple numeric vector or vector of vectors,
⍝← TestStatistic
⍝  [Av] medianTest Xv|CXv|Im
⍝⍎ 17.543 ← medianTest 2 4⍴6 3 7 0 3 7 0 8
⍝⍎ medianTest Y1 Y2 Y3 Y4
⍝⍎ (9 10 7 8/1 2 3 4) medianTest Y
     ⎕IO←0 ⋄ ⎕ML←3
     ⍺←⍬
     X←⍺{1=≡⍵:,0 1↓⍵ groupBy toNestedVector ⍺ ⋄ ⍵}⍵
     CT←{2=⍴⍴⍵:⍵
         GM←median∊⍵  ⍝ Grand Median
         B←⍵>GM
         ⍉sum each B,⍪~B}X
     N←+/,CT
     a b←+/CT
     n←+⌿CT
     T←((N*2)÷a×b)×(CT[0;]*2)+.÷n
     T-N×a÷b}
