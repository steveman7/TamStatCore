 exponential←{
  ⍝⍟ Copyright (C) 2018 by Stephen M. Mansour
  ⍝∇ Exponential Distribution; Models lifetimes or distance between events in a Poisson process.
  ⍝∊ Distribution Function; Continuous
  ⍝⍺ Lambda  T  Rate, or inverse scale
  ⍝⍺_ C Cs ['d'=Probability, 'p'=Cumulative Prob, 'q'=Inverse Distribution, 'r'=random]
  ⍝⍵ Time      T  Lifetime or Interarrival Times
  ⍝⍥ Op        oD
  ⍝⍵⍵ Rel      fR
  ⍝← Result    S  Density, Probability or Critical Value
  ⍝⍕ Result ← Lambda exponential [op [rel]] Time
  ⍝⍎ 0.03894 0.030327 0.018394 0.011157←.05 exponential 5 10 20 30 ⍝ PDF
  ⍝⍎ 0.2212 0.39347 0.63212 0.77687←.05 exponential prob ≤ 5 10 20 30 ⍝ CDF
     ⎕ML←3 ⋄ ⎕IO←0                        ⍝ Set system variables
     ⍵≡'Type':'Continuous'                ⍝
     ⍵≡'Parameters':{⍵.f←÷∘mean ⋄ ⍵}⎕NS'' ⍝
     m c←getParameters ⍺                  ⍝
     ⍵≡'Mean':÷m                          ⍝ Theoretical values
     ⍵≡'Variance':÷m*2                    ⍝       "       "
     ⍵≡'Median':(⍟2)÷m                    ⍝       "       "
     ⍵≡'Mode':0×m                         ⍝       "       "
     ⍵≡'Skewness':2*×m                    ⍝       "       "
     ⍵≡'Kurtosis':6*×m                    ⍝       "       "
     1≠≢m:∊⍣(c≠'r')⊢(m,¨c)∇¨⍵             ⍝ Make pervasive
    ⍝ str←c,'exp(⍵,⍵)'                    ⍝ Build R expression
    ⍝ rAv:+##.∆r.x str ⍵ m                ⍝ If requested, use R
     rAv:(c,'exp')rExec ⍵ m               ⍝ If requested, use R
     f←{11::0 ⋄ *-⍵×⍺}                    ⍝ Guard against large input
    ⍝c='d':(⍵≥0)×m×*-⍵×m                  ⍝ Density
     c='d':(⍵≥0)×m×m f ⍵                  ⍝ Density
    ⍝c='p':1-*-m×⍵⌈0                      ⍝ Distribution
     c='p':1-m f ⍵⌈0                      ⍝ Distribution
     c='q':-(⍟1-⍵)÷m                      ⍝ Inverse
     c='r':2-/+\(÷m)×⍟?0⍴⍨⍵+1             ⍝ Random Variable
     'Nonce Error'
 }
