 geometric←{
     ⍝⍟ Copyright (c) by Stephen M. Mansour
     ⍝∇ Geometric Distribution
     ⍝∊ Distribution Function; Discrete
     ⍝⍺ P  P  Probability of success in one trial
     ⍝⍺_ C Cs ['d'=Probability, 'p'=Cumulative Prob, 'q'=Inverse Distribution, 'r'=random]
     ⍝⍵ N   I Number of Trials
     ⍝← Q   Q Probability of at least one success in N trials
     ⍝⍕ Syntax:  Q ← P geometric N
     ⍝⍎ 0.25 ←  0.5 geometric 2
     x←⍵ ⋄ ⎕ML←3 ⋄ ⎕IO←0
     x≡'Type':'Discrete'
     3=⎕NC'⍺':∆SigErr 14 'Left argument missing'
     p c←getParameters ⍺
     ⍵≡'Mean':÷p
     ⍵≡'Mode':1
     ⍵≡'Median':⌈-÷2⍟1-p
     ⍵≡'Variance':(1-p)÷p*2
     ⍵≡'Skewness':(2-p)÷(1-p)*÷2
     ⍵≡'Kurtosis':6+(p*2)÷1-p
    ⍝str←'pdqr'[1 0 ¯1 ¯2⍳c],'geom(⍵,⍵)' ⍝ Build R expression
     1<≢p:(p,¨c)∇¨⍵
     str←c,'geom(⍵,⍵)' ⍝ Build R expression
     ⍝rAv:+##.∆r.x str(⍵-1)p                ⍝ Use R if requested
     rAv:+##.∆r.x str(⍵-c∊'dp')p          ⍝ Use R if requested
     c='d':((x>0)∧x=⌈x)×p×(1-p)*x-1        ⍝ Prob X=x
     c='p':1-(1-p)*⌊x⌈0                    ⍝ Prob X≤x
     c='q':⌊(1-p)⍟1-x                      ⍝ Inverse prob
     c='r':1+⌊(1-p)⍟?⍵⍴0                   ⍝ Random Variable
     'Nonce Error'
 }
