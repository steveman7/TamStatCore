 chiSquare←{
  ⍝⍟ Copyright (C) 2018 by Stephen M. Mansour
  ⍝∇ Chi-Square Distribution; Special case of Gamma Distribution
  ⍝∊ Distribution Function; Continuous
  ⍝⍺ DF  N Degrees of Freedom
  ⍝⍺_ C Cs ['d'=Probability, 'p'=Cumulative Prob, 'q'=Inverse Distribution, 'r'=random]
  ⍝⍥ [op] oD
  ⍝⍵⍵ [rel]  fR  Only for prob or critVal operators
  ⍝⍵ Value  S Test Statistic, probability or Sample Size
  ⍝← Result   S  Density, Probability or Critical Value
  ⍝⍕ Result ← DF chiSquare [op [rel]] Value
  ⍝⍎ 11.07 ← 5 chiSquare critVal < .05 ⍝ Value from ChiSquare Table
  ⍝⍎ 0.023379 ←   5 chiSquare prob > 13  ⍝  P-Value
  ⍝⍎ 0.097304 ← 5 chiSquare 6   ⍝ Density
     ⎕ML←3 ⋄ ⎕IO←0 ⋄ w←⍵                       ⍝ Set System variables
     w≡'Type':'Continuous'
     m c←getParameters ⍺
     w≡'Mean':m                                ⍝ Theoretical Values
     w≡'Variance':2×m                          ⍝      "        "
     w≡'Skewness':(8÷m)*÷2                     ⍝      "        "
     w≡'Kurtosis':12÷m                            ⍝      "        "
     w≡'Median':m×(1-2÷9×m)*3                     ⍝      "        "
     w≡'Mode':0⌈m-2                               ⍝      "        "
     (1≠≢m)∧c≠'r':∊(m,¨c)∇¨⍵
     str←c,'chisq(⍵,⍵)'                           ⍝ Build R expression
     rAv:+##.∆r.x str ⍵ m                         ⍝ If requested, use R
     p←(m÷2),2                                    ⍝ Paramters for gamma distribution
     c='d':p gamma 0.001⌈w                          ⍝ Chi-Square special case of gamma
     c='p':m{⍵≤0:0 ⋄
         ⍺=1:1-2×normal prob>⍵*÷2                 ⍝ 1 df
         ##.Koko.Distrib.ChiSq_P ⍵ ⍺}¨⍵  ⍝
     c='q':m{##.Koko.Distrib.ChiSq_Xa ⍵ ⍺}¨1-⍵
⍝ c=1:p{m>250:normal prob<(⍵-m)÷(2×m)*÷2
⍝         si←(⍺≡⌊⍺)∨15≥↑⍺                       ⍝ If small and integer
⍝         si:⍺ gamma prob<0⌈⍵                   ⍝ Use gamma
⍝         x←{⌊⍵+¯1 0 1}↑⍺                       ⍝ Otherwise
⍝         q←(x,¨2)gamma prob<¨⊂⍵                ⍝ find surrounding integers
⍝         z←⍺[0]{(⊃x ⍵)interpolate ⍺}¨↓⍉⊃q      ⍝ Interpolate for odd df
⍝         z}w⌊100⌈m+6×(2×m)*÷2                  ⍝
⍝     c=¯1:m{⍺≥69:⍺{z←0 1 ¯1 normal ⍵           ⍝ Inverse Chi-Square
⍝             0.5×(z+(¯1+2×⍺)*÷2)*2}w           ⍝
⍝         (⍺<33)∨0=2|⍺:(p,¯1)gamma ⍵            ⍝
⍝         x←{⌊⍵+¯1 0 1}↑p                       ⍝ find surrounding integers
⍝         q←(x,¨2)gamma criticalValue>¨⊂⍵       ⍝ Otherwise
⍝         p[0]{(⊃x ⍵)interpolate ⍺}¨↓⍉⊃q}⍵      ⍝
     1<≢m:m randomVariable¨⍵
     d←normal randomVariable m×w                ⍝ Chi-Square is sum of
     ⍝+⌿m w⍴d*2                                 ⍝ squared normal random variables
     +⌿(m,w)⍴d*2
 }
