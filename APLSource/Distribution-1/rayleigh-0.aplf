 rayleigh←{                       ⍝ Rayleigh Distribution
 ⍝⍟ Copyright (C) 2023 by Stephen M. Mansour
 ⍝∇ Rayleigh Distribution
 ⍝∊ Continuous Distribution Function
 ⍝⍺ S: Scale
 ⍝⍺ [0=Density, 1=Distribution, ¯1=Inverse Distribution, ¯2=random]
 ⍝⍵ X: Value
 ⍝← U: Density U≥0
 ⍝⍕ U ← S rayleigh X
 ⍝⍎ 0.0972 ← 5 2 rayleigh 1.3
 ⍝⍵ A partular value of the exponential random variable
     ⎕ML←3 ⋄ ⎕IO←0                       ⍝ Set system variables
     ⍵≡'Type':'Continuous'               ⍝
     pi←○1                     ⍝ Scale Parameters
     s c←getParameters ⍺
     ⍵≡'Mean':s×sqrt pi÷2                ⍝ Calculate theoretical values
     ⍵≡'Median':s×sqrt 2×⍟2              ⍝     "         "          "
     ⍵≡'Mode':s                          ⍝     "         "          "
     ⍵≡'Variance':(s*2)×0.5×4-pi
     ⍵≡'Skewness':2×(sqrt pi)×(pi-3)÷(4-pi)*3÷2
     ⍵≡'Kurtosis':-(pi⊥6 ¯24 16)÷(4-pi)*2
     c='d':0⌈(⍵÷s*2)×*-0.5×(⍵÷s)*2          ⍝ Density
     c='p':(⍵≥0)×1-*-0.5×(⍵÷s)*2            ⍝ Distribution
     c='q':s×sqrt-2×⍟1-⍵                   ⍝ Inverse
     1<≢s:(s,¨'r')∇¨⍵
     (s×sqrt 2)×(-⍟?⍵⍴0)÷2                ⍝ Random Variable
 }
