 normal←{                                ⍝ Scalar,Arithmetic⍝⍝⍝⍝⍝⍝
 ⍝⍟ Copyright (C) 2018 by Stephen M. Mansour
 ⍝∇ (Standard) Normal Distribution
 ⍝∊ Continuous Distribution Function
 ⍝⍺ Y: Mean  (default 0)
 ⍝⍺ S: Standard Deviation  (default 1)
 ⍝⍵ X: Value
 ⍝← U: Density U>0
 ⍝⍕ U ← Y S normal X
 ⍝⍎ 0.1941 ← normal 1.2
 ⍝⍎ 0.0806 ← 68 3 normal 65
     ⍺←0 1 ⋄ ⎕ML←3 ⋄ ⎕IO←0                  ⍝ Default to standard normal
     ⍵≡'Type':'Continuous'                  ⍝ Continuous Distribution
     ⍵≡'Parameters':{⍵.f←(mean,sdev) ⋄ ⍵}⎕NS'' ⍝ Get parameter estimates
     ~'X23'check ⍺:⎕SIGNAL 5                ⍝ Length error
     m s c←3↑⍺                             ⍝ Mean, sdev, action
  ⍝   m s←⍺ ⋄  ∆∆←0 ⋄ c←∆∆
     ~'S'check s:⎕SIGNAL 11                 ⍝ Sdev must be > 0
     ~'O'check c:⎕SIGNAL 11                 ⍝ Check Operator
     'Mean' 'Median' 'Mode'∊⍨⊂⍵:m           ⍝ Obtain theoretical values
     ⍵≡'Variance':s*2                       ⍝     "    "          "
     'Skewness' 'Kurtosis'∊⍨⊂⍵:0            ⍝     "    "          "
     ~'X'check ⍵:⎕SIGNAL 11                 ⍝ Right argument must be numeric
     str←'pdqr '[1 0 ¯1 ¯2⍳c],'norm(⍵,⍵,⍵)' ⍝ Build R expression
     rAv:+##.∆r.x str ⍵ m s                 ⍝ Use R if requested
     sden←{m s←⍺                            ⍝ Normal Density
         (÷s×(○2)*÷2)×*-0.5×(s÷⍨⍵-m)*2}     ⍝  e ^ (x^2)- 2
     snorm←{d←0.3193815 ¯0.3565638 1.781478 ¯1.821256 1.330274  ⍝ Cumulative Dist
         |(⍵≥0)-(0.3989423×*¯0.5×⍵*2)×d+.××⍀(5,⍴⍵)⍴÷1+0.2316419×100⌊|⍵}
     Inv←{⍺←1 2                             ⍝ Inverse operator
         f←⍺⍺{⍺-⍺⍺ ⍵}                       ⍝ Solve f(x)=0 for x
         ⍵∘f{⍺⍺ SecAlg ⍵,⍺⍺ ⍵}⍺}            ⍝ Use secant algorithm
     Rand←{u0←?⍵⍴0 ⋄ u1←?⍵⍴0                ⍝ Generate two uniform random vars
         ((-2×⍟u0)*÷2)×2○○2×u1}             ⍝ Transform and combine them
     c=0:m s sden ⍵                         ⍝ Density
     c=1:snorm(⍵-m)÷s                       ⍝ Cumulative Distribution
     c=¯3:*(m×⍵)+0.5×(s×⍵)*2                ⍝ Moment Generating function?
     c=¯2:m+s×Rand ⍵                        ⍝ Random Variable
  ⍝   c=¯2:m s(16808⌶)'Normal'⍵             ⍝ Random Variable using Ron's random variable generator
     b←⍵<0.5                                ⍝ Prob < 0.5?
     z←1 2∘(snorm Inv)¨|⍵-b                 ⍝ Find Z from P
     m+s×z×¯1*b                             ⍝ Convert Z to X
 }
