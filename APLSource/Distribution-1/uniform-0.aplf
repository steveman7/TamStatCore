 uniform←{                        ⍝ Average
     ⍝⍟ Copyright (c) by Stephen M. Mansour
     ⍝∇ Discrete Uniform Distribution
     ⍝∊ Discrete Distribution Function
     ⍝⍺ A:  Lower Bound (Default 1)
     ⍝⍺ B:  Upper Bound
     ⍝⍺ [0=Density, 1=Distribution, ¯1=Inverse Distribution, ¯2=random]
     ⍝⍵ X:  Value
     ⍝← S:  Probability: 1/(B-A-1)
     ⍝⍕ Syntax:  [A|1] B uniform I
     ⍝⍎ 0.2 ← 5 uniform 2
     ⍝⍎ 0.1 ← 11 20 uniform 17
     ⎕ML←3 ⋄ ⎕IO←0                    ⍝ Set system variables
     ⍵≡'Type':'Discrete'
     a b c←{3=⍴⍵:⍵ ⋄ 1=⍴,⍵:1,⍵,0      ⍝ Obtain parameters
         (¯1*>/⍵)↓1,⍵,0},⍺
     n←1+b-a                          ⍝ Number of possible values
     ⍵≡'Mean':(a+b)÷2                 ⍝ Calculate theoretical values
     ⍵≡'Variance':(¯1+n*2)÷12         ⍝       "        "         "
     ⍵≡'Mode':a+⍳1+b-a                ⍝       "        "         "
     ⍵≡'Skewness':0                   ⍝       "        "         "
     ⍵≡'Kurtosis':-6 5÷.×1 ¯1+n*2     ⍝       "        "         "
     c=0:({(⍵≤0)∧⍵=⌊⍵}×/⍵∘.-a,b)÷n    ⍝ Probability
     c=1:0⌈1⌊(-a-1+⌊⍵)÷n              ⍝ Cumuluative probability
     c=¯1:a+⌈n×⍵                      ⍝ Inverse
     c=¯2:a+?⍵⍴n                      ⍝ randomVariable
 }
