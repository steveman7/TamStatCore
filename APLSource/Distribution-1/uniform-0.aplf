 uniform←{                        ⍝ Average
     ⍝⍟ Copyright (c) 2024 by Stephen M. Mansour
     ⍝∇ Discrete Uniform Distribution; for continuous uniform, use rectangular
     ⍝∊ Distribution; Discrete
     ⍝⍺ [Lower←1] J Lower Bound
     ⍝⍺ Upper     J Upper Bound; must be greater than Lower
     ⍝⍺_ C Cs ['d'=Probability, 'p'=Cumulative Prob, 'q'=Inverse Distribution, 'r'=random]
     ⍝⍵ Value     J  Value
     ⍝← Q         P  Probability: 1/(B-A-1)
     ⍝⍕ Q←[Lower|_1] Upper uniform Value
     ⍝⍎ 0.2 ← 5 uniform 2      ⍝ Lower bound defaults to 1
     ⍝⍎ 0.1 ← 11 20 uniform 17
     ⎕ML←3 ⋄ ⎕IO←0                    ⍝ Set system variables
     ⍵≡'Type':'Discrete'
     ab c←getParameters ⍺
     a b←{2=≢⍵:⍵ ⋄ 1 ⍵}ab
     n←1+b-a                          ⍝ Number of possible values
     ⍵≡'Mean':(a+b)÷2                 ⍝ Calculate theoretical values
     ⍵≡'Variance':(¯1+n*2)÷12         ⍝       "        "         "
     ⍵≡'Mode':a+⍳1+b-a                ⍝       "        "         "
     ⍵≡'Skewness':0                   ⍝       "        "         "
     ⍵≡'Kurtosis':-6 5÷.×1 ¯1+n*2     ⍝       "        "         "
     c='d':({(⍵≤0)∧⍵=⌊⍵}×/⍵∘.-a,b)÷n  ⍝ Probability
     c='p':0⌈1⌊(-a-1+⌊⍵)÷n            ⍝ Cumuluative probability
     c='q':a+⌈n×⍵                     ⍝ Inverse
     1≠≡a b:(↑,¨/a b c)∇¨⍵
     c='r':a+?⍵⍴n                     ⍝ randomVariable
 }
