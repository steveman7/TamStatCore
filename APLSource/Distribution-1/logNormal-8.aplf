 logNormal←{                                ⍝ Scalar,Arithmetic⍝⍝⍝⍝⍝⍝
 ⍝⍟ Copyright (C) 2018 2023 by Stephen M. Mansour
 ⍝∇ Lognormal Distribution
 ⍝∊ Continuous Distribution Function
 ⍝⍺ X: T50 median
 ⍝⍺ U: S sigma
 ⍝⍵ Value  V>0
 ⍝← Density
 ⍝⍕ S ← X U logNormal V
 ⍝⍎ 0.11222 ← 1.2 1.5 logNormal 2.3
     ⍵≡'Type':'Continuous'
     T50 s c←3↑⍺ ⋄ ⎕ML←3 ⋄ ⎕IO←0 ⋄ ⎕DIV←1 ⍝ Set system variables
     ⍵≡'Mean':T50×*0.5×s*2                ⍝ Theoretical values
     ⍵≡'Variance':T50×*(s*2)×¯1+*s*2
     ⍵≡'Median':T50
     ⍵≡'Mode':T50÷*s*2
     ⍵≡'Skewness':{(⍵+2)×(⍵-1)*÷2}*s*2
     ⍵≡'Kurtosis':¯6+1 2 3+.×*4 3 2×s*2
     str←'pdqr'[1 0 ¯1 ¯2⍳c],'lnorm(⍵,⍵,⍵)'
    ⍝ #.R_Available:+#.∆r.x str ⍵ m s
     rAv:+##.∆r.x str ⍵(⍟T50)s      ⍝
     U←s÷⍨⍟(⍵÷T50)⌈÷⌊/⍳0                  ⍝ ln(t/T50)÷s
     c=0:(*-0.5×U*2)÷s×⍵×sqrt○2           ⍝ Density
     c=1:normal prob<U                    ⍝ Cumulative Distribution
     c=¯1:T50×*s×normal critVal>⍵         ⍝ Inverse
     c=¯2:T50×*s×normal randomVariable ⍵  ⍝ Random variable
 }
