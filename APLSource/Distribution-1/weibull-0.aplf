 weibull←{                       ⍝ Weibull Distribution
 ⍝⍟ Copyright (C) 2018 by Stephen M. Mansour
  ⍝∇ Weibull Distribution; Reliability Model
  ⍝∊ Distribution Function; Continuous
  ⍝⍺ Shape  S  Controls the shape of the distribution
  ⍝⍺ Scale  T  Characteristic Life; 63.2 percentile
  ⍝⍺_ C Cs ['d'=Probability, 'p'=Cumulative Prob, 'q'=Inverse Distribution, 'r'=random]
  ⍝⍵ Time      T  Lifetime or Age
  ⍝⍥ Op        oD
  ⍝⍵⍵ Rel      fR
  ⍝← Result    S  Density, Probability or Critical Value
  ⍝⍕ Result ← Shape Scale weibull [op [rel]] Time
  ⍝⍎ 1.7977E308 0.0034865 0.0018394 0.0011996 0.00085955 0.00065061←0.5 100 weibull 0 50 100 150 200 250 ⍝ Shape < 1; PDF decreases from infinity
  ⍝⍎ 1 0.0060653 0.0036788 0.0022313 0.0013534 0.00082085←1 100 weibull 0 50 100 150 200 250  ⍝ Shape = 1; PDF decreases from 1/characteristic life
  ⍝⍎ 0 0.0046971 0.014715 0.00085451 3.6011E¯8 6.7803E¯18←4 100 weibull 1 50 100 150 200 250  ⍝ Shape > 1; PDF increases from 0,then decreases
  ⍝⍎ 0 0.060587 0.63212 0.99367 1←4 100 weibull prob ≤ 0 50 100 150 200 ⍝ CDF; F(t)
  ⍝⍎ 1 0.93941 0.36788 0.0063297 1.1254E¯7←4 100 weibull prob > 0 50 100 150 200 ⍝ Reliability or Survivor Function R(t)=1-F(t)
     ⎕ML←3 ⋄ ⎕IO←0                          ⍝ Set system variables
     ⍵≡'Type':'Continuous'                  ⍝
    ⍝m k c←3↑⍺                              ⍝ Scale, Shape Parameters
     (m k)c←getParameters ⍺
     ⍵≡'Mean':k×!÷m                         ⍝ Calculate theoretical values
     ⍵≡'Median':k×(⍟2)*÷m                   ⍝     "         "          "
     ⍵≡'Mode':(m≥1)×k×(1-÷m)*÷m             ⍝     "         "          "
     ⍵≡'Variance':(k*2)×(!2 1÷m)-.*1 2
     ⍵≡'Skewness':m{x←⍺ ⍵ weibull'Mean'
         v←⍺ ⍵ weibull'Variance'
         (v*-3÷2)×((!3÷⍺)×⍵*3)-(3×x×v)+x*3}k
     ⍵≡'Kurtosis':'Nonce Error'
     2=≡m k:(↑,¨/⍺)∇¨⍵                      ⍝ PervasiveLeft argument
     ⍝str←c,'weibull(⍵,⍵,⍵)'
⍝     rAv:+##.∆r.x str ⍵ m k
     rAv:(c,'weibull')rExec ⍵ m k
     c='d':{z←((t÷k)*m)×(*-(t÷k)*m)×m÷t←⍵⌈⎕CT ⍝ Density
         z←(⌊/(1+×m-1)↑1 0)@(⍸⍵=0),z        ⍝ Replace f(0) with ¯ 1 or 0
         (⍴⍵)⍴z×⍵≥0}⍵                       ⍝ P(X<0)=0
     c='p':(⍵≥0)×1-*-(⍵÷k)*m                  ⍝ Distribution
     c='q':k×(-⍟1-⍵)*÷m                      ⍝ Inverse
     k×(-⍟?⍵⍴0)÷m                           ⍝ Random Variable
 }
