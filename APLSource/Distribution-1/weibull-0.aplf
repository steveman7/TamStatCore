 weibull←{                       ⍝ Weibull Distribution
 ⍝⍟ Copyright (C) 2018, 2023 by Stephen M. Mansour
 ⍝∇ Weibull Distribution
 ⍝∊ Continuous Distribution Function
 ⍝⍺ M: Shape
 ⍝⍺ K: Scale
 ⍝⍺ [0=Density, 1=Distribution, ¯1=Inverse Distribution, ¯2=random]
 ⍝⍵ X: Value
 ⍝← U: Density U≥0
 ⍝⍕ U ← M K weibull X
 ⍝⍎ 0.0972 ← 5 2 weibull 1.3
 ⍝⍵ A partular value of the exponential random variable
     ⎕ML←3 ⋄ ⎕IO←0                          ⍝ Set system variables
     ⍵≡'Type':'Continuous'                  ⍝
     m k c←3↑⍺                              ⍝ Scale, Shape Parameters
     ⍵≡'Mean':k×!÷m                         ⍝ Calculate theoretical values
     ⍵≡'Median':k×(⍟2)*÷m                   ⍝     "         "          "
     ⍵≡'Mode':(m≥1)×k×(1-÷m)*÷m             ⍝     "         "          "
     ⍵≡'Variance':(k*2)×(!2 1÷m)-.*1 2
     ⍵≡'Skewness':m{x←⍺ ⍵ weibull'Mean'
         v←⍺ ⍵ weibull'Variance'
         (v*-3÷2)×((!3÷⍺)×⍵*3)-(3×x×v)+x*3}k
     ⍵≡'Kurtosis':'Nonce Error'
     str←'pdqr'[1 0 ¯1 ¯2⍳c],'weibull(⍵,⍵,⍵)'
     ⍝#.R_Available:+#.∆r.x str ⍵ m k
     rAv:+##.∆r.x str ⍵ m k
     ⍝c=0:(m÷⍵)×((⍵÷k)*m)×*-(⍵÷k)*m         ⍝ Density
     c=0:{z←((t÷k)*m)×(*-(t÷k)*m)×m÷t←⍵⌈⎕CT ⍝ Density
         z←(⌊/(1+×m-1)↑1 0)@(⍸⍵=0),z        ⍝ Replace f(0) with ¯ 1 or 0
         (⍴⍵)⍴z×⍵≥0}⍵                       ⍝ P(X<0)=0
     c=1:(⍵≥0)×1-*-(⍵÷k)*m                  ⍝ Distribution
     c=¯1:k×(-⍟1-⍵)*÷m                      ⍝ Inverse
     k×(-⍟?⍵⍴0)÷m                           ⍝ Random Variable
 }
