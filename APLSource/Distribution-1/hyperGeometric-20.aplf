 hyperGeometric←{                                ⍝ Scalar,Arithmetic
     ⍝⍟ Copyright (c) by Stephen M. Mansour
     ⍝∇ Hypergeometric Distribution
     ⍝∊ Distribution Function; Discrete
     ⍝⍺ K I  Successes in population
     ⍝⍺ M M  Sample size
     ⍝⍺ N N  Population size
     ⍝⍵ X I  Number of successes in sample
     ⍝← P P  Probability of X successes
     ⍝⍕ Syntax:  K M N hyperGeometric X
     ⍝⍎ 0.03993 ← 4 5 52 hyperGeometric 2 ⍝ Probability of 2 aces in a poker hand
     ⍝⍎ 0.12469 ← 13 13 52 hyperGeometric 5 ⍝ Probability of 5 card spade suit in a bridge hand
     'Type'≡x←⍵:'Discrete'
    ⍝ m n N c←4↑⍺ ⋄ ⎕ML←3 ⋄ ⎕IO←0             ⍝ Set system variables
     ⎕ML←3 ⋄ ⎕IO←0
     (m n N)c←getParameters ⍺
   ⍝  m n N∨.<0 1 1:∆SigErr'All parameters must be positive'
     ∨/∊m n N<0 1 1:∆SigErr'All parameters must be positive'
     x≡'Mean':m×n÷N                           ⍝ Theoretical Values
     x≡'Median':m n N ∇ critVal<0.5           ⍝    "           "
     x≡'Variance':N{×/(⍵,⍺-⍵),÷⍺-3↑1}m n      ⍝    "           "
     x≡'Mode':⌊(m n×.+1)÷N+2                  ⍝    "           "
     x≡'Skewness':N{z←(⍺-1)÷×/⍵,⍺-⍵           ⍝    "           "
         (z*÷2)×(×/⍺-2×⍵)÷⍺-2}m n             ⍝
     x≡'Kurtosis':N{u←×/⍵,⍺-⍵                 ⍝    "           "
         z←(⍺-1)×(⍺*2)×(⍺×⍺+1)-6+.×⍵×⍺-⍵      ⍝
         z+←6×u×⍺⊥5 ¯6                        ⍝
         z÷×/u,⍺-2 3}m n                      ⍝
    ⍝ str←'pdqr'[1 0 ¯1 ¯2⍳c],'hyper(⍵,⍵,⍵,⍵)' ⍝ Build R Expression
     str←c,'hyper(⍵,⍵,⍵,⍵)' ⍝ Build R Expression
     rAv:+##.∆r.x str ⍵ m(N-m)n               ⍝ Use R if requested
     1≠≡m n N:(↑,¨/m n N c)∇¨⍵
     ⍝c='d':{p←((⍵!m)×(n-⍵)!N-m)÷n!N             ⍝ Probability (X=x)
     c='d':{p←{↑(!/-⍨/⍵)×÷/!⌿⍵}⍵ m,⍪n N
         p×(⍵=⌈⍵)∧⍵≥0}⍵                       ⍝
     c='p':(x≥0)×(⊂⌊0⌈x)⌷1{⍺>⌈/x:+\⍵            ⍝ Cumulative Probability X≤x
         11::+\m n N hyperGeometric⍳1+⌈/x     ⍝ Use brute force if ÷0
         y←(m n×.-⍺-1)÷⍺×⍺+N-m+n              ⍝ y=P(x+1)÷P(x)
         (⍺+1)∇ ⍵,⍵[⍺-1]×y},÷/n!⌽-\N m        ⍝
    ⍝c='q':+/x∘.≥1{(+/⍵)>⌈/x:+\⍵             ⍝ Critical Value???
     c='q':1+x⍸⍨1{(+/⍵)>⌈/x:+\⍵               ⍝ Critical Value???
         11::+\m n N hyperGeometric⍳m⌊n     ⍝ Use brute force if ÷0
         y←(m n×.-⍺-1)÷⍺×⍺+N-m+n              ⍝ y=P(x+1)÷P(x)
         ⍝  11::+\m n N hyperGeometric⍳m⌊n    ⍝ Use brute force if ÷0
         (⍺+1)∇ ⍵,⍵[⍺-1]×y},÷/n!⌽-\N m        ⍝
     c='r':{m+.>n?N}¨⍳⍵                       ⍝ Random Variables
     'Nonce Error'
 }
