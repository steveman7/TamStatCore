 poisson←{
     ⍝⍟ Copyright (c) 2024 by Stephen M. Mansour
     ⍝∇ Poisson Distribution
     ⍝∊ Distribution Function; Discrete
     ⍝⍺ Lambda S Mean Rate
     ⍝⍺ P  P  Probability of success
     ⍝⍺_ C Cs ['d'=Probability, 'p'=Cumulative Prob, 'q'=Inverse Distribution, 'r'=random]
     ⍝⍵ N   N Number of items in a particular interval
     ⍝← P   P Probability of exactly X successes in that interval
     ⍝⍕ Syntax:  Lambda poisson N
     ⍝⍎ 0.104444863 ← 5  poisson 7
     x←⍵ ⋄ ⎕ML←3 ⋄ ⎕IO←0                  ⍝ Set system variables
     x≡'Type':'Discrete'                  ⍝ Get parameter
     m c←getParameters ⍺
     (⊂x)∊'Mean' 'Variance':m             ⍝ Obtain theoretical values
     x≡'Median':⌊m+1 0.02-.÷3 m           ⍝   "       "           "
     x≡'Mode':y⊃⍨</m ∇ y←(⌈¯1+m),⌊m       ⍝   "       "           "
     x≡'Skewness':m*-÷2                   ⍝   "       "           "
     x≡'Kurtosis':÷m                      ⍝   "       "           "
    ⍝ str←c,'pois(⍵,⍵)'                    ⍝ Build R Expression
    ⍝ rAv:+##.∆r.x str ⍵ m                 ⍝ Call R if requested
     rAv:(c,'pois')rExec ⍵ m              ⍝ Call R if requested
     1<≢m:(m,¨c)∇¨⍵
     c='d':m{p←(*-⍺)×(⍺*⍵)÷!⍵⌈0           ⍝ Probability (X=x)
         p×(⍵=⌊⍵)∧⍵≥0}x                   ⍝ p=0 unless natural number
     c='p':(x≥0)×(⊂⌊0⌈x)⌷1{⍺>⌈/x:+\⍵      ⍝ Cumulative Pr(X≤x)
         (⍺+1)∇ ⍵,⍵[⍺-1]×m÷⍺},*-m         ⍝ Factor = Pr(x+1)÷Pr(x)
     c='q':+/x∘.>1{(+/⍵)≥⌈/x:+\⍵          ⍝ Inverse probability
         (⍺+1)∇ ⍵,⍵[⍺-1]×m÷⍺},*-m         ⍝ Factor = Pr(x+1)÷Pr(x)
     c='r':x⍴m{t←⍺'r'exponential⌈⍵×⍺+2×⍺*÷2 ⍝ Poisson Random Variable
         t0←+/(+\t)∘.>1+⍳⍵ ⋄ +⌿t0∘.=⍳⍵}×/x  ⍝
 }
