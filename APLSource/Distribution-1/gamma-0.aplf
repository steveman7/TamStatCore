 gamma←{                ⍝ Gamma Distribution
  ⍝⍟ Copyright (c) by Stephen M. Mansour
  ⍝∇ Gamma Distribution
  ⍝∊ Distribution Function; Continuous
  ⍝⍺ K      T  Shape Parameter 1
  ⍝⍺ Theta  T  Scale Parameter 2
  ⍝⍺_ C Cs ['d'=Probability, 'p'=Cumulative Prob, 'q'=Inverse Distribution, 'r'=random]
  ⍝⍥ [op] oD
  ⍝⍵⍵ [rel]  fR  Only for prob or critVal operators
  ⍝⍵ X        T   Probability, CriticalValue or sampleSize
  ⍝← Result   S  Density, Probability or Critical Value
  ⍝⍕ Result ← K Theta beta [op [rel]] X
  ⍝⍎ 0.065642 ← 4 2 gamma prob < 3   ⍝ Cumulative Distribution Function
  ⍝⍎ 0.062755 ← 4 2 gamma 3          ⍝ Density
    ⍝ 11::0
     x←⍵ ⋄ ⎕ML←3 ⋄ ⎕IO←0              ⍝ Set system variables
     x≡'Type':'Continuous'
     (k r)c←getParameters ⍺           ⍝ Get parameters
     x≡'Mean':k×r                     ⍝ Calculate theoretical values
     x≡'Variance':k*r*2               ⍝      "         "         "
     x≡'Median':k r ∇ critVal<0.5     ⍝      "         "         "
     x≡'Mode':r×k-1                   ⍝      "         "         "
     x≡'Skewness':2÷k*÷2              ⍝      "         "         "
     x≡'Kurtosis':6÷k                 ⍝      "         "         "
     2=≡k r:(1⌽¨↑,¨/c k r)∇¨⍵         ⍝ Left argument pervasive
     str←c,'gamma(⍵,⍵,⍵)'             ⍝ Build R expression
     rAv:+##.∆r.x str ⍵ k r           ⍝ Use R if requested
     F←k r∘{                          ⍝ Cumulative distribution
         k r←⍺ ⋄ c←⍵÷r
         g←{x←⍵
             s←0{y←*(⍺×⍟x⌈⎕CT)-⍟!k+⍺
                 (170≤k+⍺)∨(⍺>10)∧⎕CT>|y:⍵
                 (⍺+1)∇ ⍵+y}0
             s×*(⍺×⍟⍵⌈⎕CT)+(⍟!⍺-1)-⍵}
         k≠⌊k:|(k g¨c)÷!k-1
         i←⍳k
         |1-+/*(-⍟!i)+(i×⍟⎕CT⌈c)-c}
     Finv←{⍵≤0:0
         P0←k r{m←(×/⍺)-↑⍺                  ⍝ Estimate mean
             s←⍺×.*0.5 1                    ⍝ Estimate std deviation
             (m>2)∨⍵>0.5:m+s×1 2*¯1*⍵<0.5
             m=1.5:0.05 1.5
             m<2:0.0002 0.0001
             (m=2)∧⍵<0.1:0.2 0.8
             (m=2.5)∧⍵<0.025:0.4 0.6
             m+0 1}⍵
         G←⍵∘{⍺-↑F ⍵}¨
         G SecAlg P0,G P0}
     c='d':(r*-k)×(x*k-1)×(*-x÷r)÷!k-1        ⍝ Density
     c='p':F¨x                                ⍝ Distribution
     c='q':Finv¨x                             ⍝ Inverse
     k r(16808⌶)'Gamma'⍵                      ⍝ Random Variable
 }
