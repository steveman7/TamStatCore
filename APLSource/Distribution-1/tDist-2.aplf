 tDist←{                ⍝ t Distribution
 ⍝⍟ Copyright (C) 2018 by Stephen M. Mansour
  ⍝∇ Student t Distribution
  ⍝∊ Distribution Function; Continuous
  ⍝⍺ DF     N  Degrees of Freedom
  ⍝⍺_ C Cs ['d'=Probability, 'p'=Cumulative Prob, 'q'=Inverse Distribution, 'r'=random]
  ⍝⍥ [operator] oD
  ⍝⍵⍵ [rel]  fR  Only for prob or critVal operators
  ⍝⍵ Value  S Test Statistic, probability or Sample Size
  ⍝← Result   S  Density, Probability or Critical Value
  ⍝⍕ Result ← DF tDist [operator [rel]] Value
  ⍝⍎ 2.015← 5 tDist critVal < .05 ⍝  T-table Value, one-sided
  ⍝⍎ ¯2.5706 2.5706←5 tDist critVal between .05 ⍝ T-Table value Two-sided
  ⍝⍎ 0.34886←5 tDist prob > 2.3            ⍝ P-Value; one-sided
  ⍝⍎ 0.069772←5 tDist prob outside 2.3     ⍝ P-Value; two-sided
  ⍝⍎ 0.043551←5 tDist 2.3                  ⍝ Density
     ⎕ML←3 ⋄ ⎕IO←0 ⋄ w←⍵                           ⍝ Set system variables
     w≡'Type':'Continuous'
     ⍝m c←2↑⍺
    ⍝ m←{↑⍣(1=≢⍵)⊢⍵}(-1≥c←↑⌽⍺)↓⍺
     m c←getParameters ⍺
   ⍝  c←c×c≤1                             ⍝ Parameter (D.F)
   ⍝ c←checkOperator
    ⍝ w≡'Mean':{⍵>1:0 ⋄ ⎕NULL}m          ⍝ Calculate theoreetical value
     w≡'Mean':(⎕NULL 0)[m>1]             ⍝ Calculate
    ⍝ w≡'Variance':{⍵>2:⍵÷⍵-2 ⋄ ⎕NULL}m  ⍝    "          "          "
     w≡'Variance':⎕NULL@{m≤2}m÷1⌈m-2     ⍝    "          "          "
     w≡'Mode':0                          ⍝    "          "          "
     w≡'Skewness':(⎕NULL 0)[m>3]
    ⍝w≡'Kurtosis':{⍵≤4:⌊/⍳0 ⋄ 6÷⍵-4}m    ⍝    "          "          "
     w≡'Kurtosis':{⌊/⍳0}@{⍵≤4}6÷¯4+m+m=4 ⍝    "          "          "
    ⍝ 1≠≢m:∊⍣(c≠¯2)⊢(m,¨c)∇¨⍵             ⍝ Make pervasive
     1≠≢m:∊⍣(c≠'r')⊢(m,¨c)∇¨⍵             ⍝ Make pervasive
     str←c,'t(⍵,⍵)'                      ⍝ Build R expression
     rAv:+##.∆r.x str ⍵ m                ⍝ Use R if requestedion
     TDEN←{Z←÷⌿!¯1+0.5×1 0∘.+K←⍺⌊342     ⍝ Density Function
         Z←Z÷(○K)*÷2
         Z÷(1+(⍵*2)÷K)*(K+1)÷2}
     INTEGRAL←{AB←¯2↑0,⍵                 ⍝ Definite Integral
         H←(-/⌽AB)÷N←⍺
         XI0←+/⍺⍺ AB
         XI←((N÷2),2)⍴0,⍺⍺ H×1↓⍳N
         H×(1 2 4+.×XI0,+⌿XI)÷3}
     PRIME←{∆X←0.000001×⌊/X←,⍵                      ⍝ Derivative
         Z←(2⍴⍴X)⍴X
         Z←⍺⍺ X,[0]Z+∆X×N∘.=N←⍳⍴X
         ⍉((1 0↓Z)-Z[(⍴X)/0;])÷∆X}
     INV←{TOL←0.0001 ⋄ ⍺←1 ⋄ ⎕DIV←1                 ⍝ Inverse Operator
         FX←(⍺⍺ ⍺)-⍵
         X←⍺-FX÷⍺⍺ PRIME ⍺
         TOL<⌈/|X-⍺:X ∇ ⍵ ⋄ X}
     SNORM←{d←0.3193815 ¯0.3565638 1.781478 ¯1.821256 1.330274
         |(⍵≥0)-(0.3989423×*¯0.5×⍵*2)×d+.××⍀(5,⍴⍵)⍴÷1+0.2316419×100⌊|⍵}
     TDIST←{⍺>1001:SNORM ⍵                          ⍝ Cumulative Distribution
         0.5+(×⍵)×100(⍺∘TDEN)INTEGRAL¨|⍵}
     TINV←{↑¨(⍺∘TDIST)INV¨⍵}                        ⍝ Critical Value
     c='d':m{⍺=1:÷○(1+⍵)*2 ⋄ ⍺ TDEN ⍵}w               ⍝ Density
     c='p':m{##.Koko.Distrib.Student_P ⍵,⌊⍺}¨w
     c='q':m{⍵<0.5:-##.Koko.Distrib.Student_Tad ⍵,⌊⍺
         ##.Koko.Distrib.Student_Tpd ⍵,⌊⍺}¨w
     Y←normal randomVariable w                      ⍝ Normal Random Variable
     Z←m chiSquare randomVariable w                 ⍝ Chi Square random Variable
     Y÷(Z÷m)*÷2                                     ⍝ t Random Variable
 }
