 wilcoxonT←{                                ⍝ Scalar,Arithmetic
     ⍝⍟ Copyright (c) 2021, 2025 by Stephen M. Mansour
     ⍝∊ Table A12 QUantiles of Wilcoxon Signed Ranks Test
     ⍝∊ Conover, Practical Nonparametric Statistics Table 12, p. 545
     ⍝∇ Wilcoxon T distribution
     ⍝∊ Distribution Function; Discrete
     ⍝⍺ N N Sample Size
     ⍝⍵ R N Value of statistic
     ⍝← P P Probability
     ⍝⍕ P ← N wilcoxonT R
     ⍝⍎ 0.0079 0.031746 ←  5 wilcoxonT 2 3
     'Type'≡x←⍵:'Discrete'
     ⎕ML←3 ⋄ ⎕IO←0                                  ⍝ Set system variables
     N C←getParameters ⍺
     1≠≢N:(N,¨C)∇¨⍵                                 ⍝ Multiple
     M←0.25×N×N+1                                   ⍝ Mean of distribution
     RF V←{⍵>10:0,24÷⍨⍵×(⍵+1)×1+2×⍵                 ⍝ Large sample variance
         RF←relative 1 frequency(1+⍳⍵)+.×(⍵/2)⊤⍳2*⍵ ⍝ Small sample frequency
         RF(var RF)}N                               ⍝   and variance
     x≡'Mean':M                                     ⍝ Theoretical Values
     x≡'Variance':V                                 ⍝    "           "
     x≡'Mode':M                                     ⍝    "           "
     x≡'Skewness':0                                 ⍝    "           "
     x≡'Kurtosis':N{⍺>10:0 ⋄ kurtosis ⍵×⍤1⊢1,2*⍺}RF ⍝    "           "
     S←sqrt V                                       ⍝ Standard deviation
     C='d':N{⍺>10:M S normal ⍵ ⋄ RF[⍵;1]}⍵          ⍝ Density
     C='p':N{⍺>10:M S normal prob≤⍵+0.5             ⍝ Cumulative Prob
         +/(⍵+1)↑RF[;1]}⍵                           ⍝   Small sample
     C='q':N{⍵=0.5:M
         ⍺≤10:1+(+\RF[;1])⍸⍵
         ⍵<0.5:⌊M S normal critVal>⍵                ⍝ Critical Value
         ⍵>0.5:⌈M S normal critVal>⍵}¨⍵             ⍝   Small sample
     C='r':(?(⍵,N)⍴2)+.×1+⍳N                        ⍝ Random Variables


 }
