 binomial←{
     ⍝⍟ Copyright (c) by Stephen M. Mansour
     ⍝∇ Binomial Distribution
     ⍝∊ Distribution Function; Discrete
     ⍝⍺ N  N  Number of Trials
     ⍝⍺ P  P  Probability of success
     ⍝⍺_ C Cs ['d'=Probability, 'p'=Cumulative Prob, 'q'=Inverse Distribution, 'r'=random]
     ⍝⍵ X   I Number of successes
     ⍝← Q   Q Probability of X successes
     ⍝⍕ Syntax:  Q ← N [P|_0.5] binomial X
     ⍝⍎ 0.27344 ← 7 0.5 binomial 3        ⍝ Probability of exactly 3 heads in 7 coint tosses
     ⍝⍎ 0.003215 ← 5 (÷6) binomial 4      ⍝ Probability of exactly 4 sixes in a toss of 5 dice.
     ⍺←1 0.5 ⋄ x←⍵ ⋄ ⎕ML←3 ⋄ ⎕IO←0        ⍝ Set system variables
     x≡'Type':'Discrete'                  ⍝
     (n p)c←getParameters ⍺               ⍝
     n p←n{⍺≠⍵:⍺ ⍵ ⋄ ⍺=⌊⍺:⍺ 0.5 ⋄ 1 ⍵}p   ⍝ Default n=1; p=0.5
     ⍝ (c≠¯2)∧~'X123'check ⍺:⎕SIGNAL 5    ⍝ Parameters must be numeric, 2 chars
     ⍝ ~'O'check c:⎕SIGNAL 11             ⍝ Operator domain error
     ⍝  ~'IU'check n:⎕SIGNAL 11           ⍝ N must be positive interger
     ⍝     ~'Q'check p:⎕SIGNAL 11         ⍝ P must be a probability
     ⍝ 1<≡n p:n ∇{⍺ ⍵ c ⍺⍺ x}¨p           ⍝ If multiple n, expand for simulation
     x≡'Mean':n×p                         ⍝
     x≡'Variance':n×p×1-p                 ⍝
     x≡'Median':y←↑{(⍵≥0.5)/⍵}n p ∇ prob≤y←(⌊,⌈)n×p
     x≡'Mode':y⊃⍨</n p ∇ y←¯1 0+⌊p×n+1
     x≡'Skewness':(1-2×p)÷(n×p×1-p)*÷2
     x≡'Kurtosis':1 0÷.+¯6 n×p×1-p
     2=≡n p:(1⌽¨c,¨n,¨p)∇¨⍵               ⍝ Left argument pervasive
     ~'X'check ⍵:⎕SIGNAL 11               ⍝ Right argument must be numeric
     str←c,'binom(⍵,⍵,⍵)'                 ⍝ Build R expression
     rAv:+##.∆r.x str ⍵ n p               ⍝ If requested, use R
     ⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝⍝
     f←{(⍵!n)×(p*⍵)×(1-p)*0⌈n-⍵}          ⍝ Binomial formula
     c='d':(⍵=⌊⍵)×f ⍵                     ⍝ Probability X=x
     (c='p')∧n>200:+/¨f⍳¨⌊1+⍵             ⍝ Cumulative for Large n
     c='p':(⍵≥0)×(⊂⌊0⌈⍵)⌷0{⍺≥⌈/x:+\⍵      ⍝ Cumulative Probability X≤x
         y←(p÷1-p)×(n-⍺)÷⍺+1              ⍝ y=P(x+1)÷P(x)
         (⍺+1)∇ ⍵,⍵[⍺]×y},(1-p)*n         ⍝
     c='q':+/x∘.>0{(+/⍵)≥⌈/x:+\⍵          ⍝ Critical Value
         y←(p÷1-p)×(n-⍺)÷⍺+1              ⍝ y=P(x+1)÷P(x)
         (⍺+1)∇ ⍵,⍵[⍺]×y},(1-p)*n         ⍝
     c='r':p+.>?(n,⍵)⍴0                   ⍝ Random Variable
     'Nonce Error'
 }
